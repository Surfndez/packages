# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=mate-menus
pkgver=1.18.0
_pkgver=${pkgver%.*}
pkgrel=2
pkgdesc='MATE menu specifications'
arch=('x86_64')
url='http://mate-desktop.org/'
license=('GPL' 'LGPL')
groups=('mate')
depends=('glib')
makedepends=('intltool' 'python2' 'gobject-introspection')
optdepends=('python2: python2 bindings')
source=("http://pub.mate-desktop.org/releases/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('b66092d08b7ec18845f04fcdf0b498d5')

# todo: rebuild with depends

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	export PYTHON=/usr/bin/python2
	
	./configure --prefix=/usr \
		--sysconfdir=/etc \
        --localstatedir=/var \
        --enable-python

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
