# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=mate-session-manager
pkgver=1.18.1
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='The MATE Session Handler'
arch=('x86_64')
url='http://mate-desktop.org/'
license=('GPL')
groups=('mate')
depends=('dbus-glib' 'gtk3' 'libsm' 'mate-desktop')
makedepends=('intltool' 'xtrans' 'atk-dev')
optdepends=('gnome-keyring: keyring support'
			'xdg-user-dirs-gtk: manage user directories')
source=("http://pub.mate-desktop.org/releases/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('f7ed15ec600ef96aae1f741eee05e7c8')

# todo: rebuild with depends
# elogind support?

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--libexecdir=/usr/lib/${pkgname} \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-upower

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
