# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libmatemixer
pkgver=1.18.0
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='Mixer library for MATE Desktop'
arch=('x86_64')
url='http://mate-desktop.org/'
license=('LGPL')
depends=('glib' 'libalsa')
makedepends=('intltool' 'alsa-lib-dev')
source=("http://pub.mate-desktop.org/releases/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('ec4dcf758b7dcba6c602e6ce8bf280f9')

# todo: rebuild with libpulse

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
