# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libmatekbd
pkgver=1.19.0
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='MATE keyboard library'
arch=('x86_64')
url='http://mate-desktop.org/'
license=('LGPL')
depends=('gtk3' 'libxklavier')
makedepends=('intltool' 'atk-dev')
source=("http://pub.mate-desktop.org/releases/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('15f696d2d756a694e02ef6dc1580337b')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	
	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
