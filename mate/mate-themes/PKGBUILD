# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=mate-themes
pkgver=3.22.14
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='Default themes for the MATE desktop'
arch=('any')
url='http://mate-desktop.org/'
license=('GPL')
groups=('mate')
makedepends=('gtk2' 'intltool')
optdepends=('gtk-engines: for gtk2 themes'
			'gtk-engine-murrine: for gtk2 themes'
			'mate-icon-theme: default icon theme')
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/themes/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('861a92c0722c6dafe724fbf4e4df803b')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
