# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=caja-extensions
pkgname=('caja-extensions-common' 'caja-gksu' 'caja-image-converter' 'caja-open-terminal' 'caja-sendto' 'caja-share' 'caja-wallpaper' 'caja-xattr-tags')
pkgver=1.19.0
pkgrel=1
pkgdesc="Caja extensions"
url="http://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('caja' 'dbus-glib')
#'gksu' 'gupnp' 'imagemagick' 'samba'
makedepends=('intltool' 'atk-dev' 'at-spi2-atk-dev')
source=("http://pub.mate-desktop.org/releases/${pkgver%.*}/$pkgbase-$pkgver.tar.xz")
sha1sums=('fad076e77af5f3dba3a80b11d6db446b16f193ce')

build() {
	cd ${pkgbase}-${pkgver}
	PYTHON=/usr/bin/python2 ./configure \
		--prefix=/usr

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	make
}

package_caja-extensions-common() {
	pkgdesc="Caja extensions (common files)"
	depends=()

	cd ${pkgbase}-${pkgver}
	make -C po DESTDIR="${pkgdir}" install
}

package_caja-gksu() {
	pkgdesc="Privilege granting extension for Caja using gksu"
	depends=('caja' 'caja-extensions-common' 'gksu')
	groups=('mate-extra')

	cd ${pkgbase}-${pkgver}
	make -C gksu DESTDIR="${pkgdir}" install
}

package_caja-image-converter() {
	pkgdesc="A Caja extension for simple image conversions"
	depends=('caja' 'caja-extensions-common' 'imagemagick')
	groups=('mate-extra')

	cd ${pkgbase}-${pkgver}
	make -C image-converter DESTDIR="${pkgdir}" install
}

package_caja-open-terminal() {
	pkgdesc="A Caja extension for opening terminals in arbitrary local paths"
	depends=('caja' 'caja-extensions-common')
	groups=('mate-extra')

	cd "${srcdir}/${pkgbase}-${pkgver}"
	make -C open-terminal DESTDIR="${pkgdir}" install
}

package_caja-sendto() {
	pkgdesc="A Caja extension for sending files"
	depends=('caja' 'caja-extensions-common' 'dbus-glib' 'gupnp')
	groups=('mate-extra')
	optdepends=('pidgin: Support for sending to instant messaging networks.'
				'gajim: Support for sending to Jabber.')

	cd ${pkgbase}-${pkgver}
	make -C sendto DESTDIR="${pkgdir}" install
}

package_caja-share() {
	pkgdesc="A Caja extension to quickly share a folder"
	depends=('caja' 'caja-extensions-common' 'samba')
	groups=('mate-extra')  

	cd ${pkgbase}-${pkgver}
	make -C share DESTDIR="${pkgdir}" install
}

package_caja-wallpaper() {
	pkgdesc="A Caja extension to quickly set wallpaper"
	depends=('caja' 'caja-extensions-common')
	groups=('mate-extra')
	
	cd ${pkgbase}-${pkgver}
	make -C wallpaper DESTDIR="${pkgdir}" install
}

package_caja-xattr-tags() {
	pkgdesc="A Caja extension to see tags stored on xattrs"
	depends=('caja' 'caja-extensions-common')
	groups=('mate-extra')
	
	cd ${pkgbase}-${pkgver}
	make -C xattr-tags DESTDIR="${pkgdir}" install
}
