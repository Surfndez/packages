# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=mate-desktop
pkgver=1.18.0
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='Library with common API for various MATE modules'
arch=('x86_64')
url='http://mate-desktop.org/'
license=('GPL' 'LGPL')
groups=('mate')
depends=('gtk3' 'startup-notification' 'dconf')
makedepends=('intltool' 'atk-dev')
source=("http://pub.mate-desktop.org/releases/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('3252cd41ec927805398bc812841215fd')

# todo: rebuild with gobject-introspection

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--disable-schemas-compile

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
