# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=openrc
pkgver=0.31
pkgrel=1
pkgdesc="An init and service control software"
arch=('x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
groups=('cromnix-base')
depends=('kbd' 'procps-ng' 'psmisc' 'sed')
backup=('etc/openrc/rc.conf'
        'etc/openrc/conf.d/bootmisc'
        'etc/openrc/conf.d/consolefont'
        'etc/openrc/conf.d/devfs'
        'etc/openrc/conf.d/dmesg'
        'etc/openrc/conf.d/fsck'
        'etc/openrc/conf.d/hostname'
        'etc/openrc/conf.d/hwclock'
        'etc/openrc/conf.d/keymaps'
        'etc/openrc/conf.d/killprocs'
        'etc/openrc/conf.d/localmount'
        'etc/openrc/conf.d/modules'
        'etc/openrc/conf.d/mtab'
        'etc/openrc/conf.d/netmount'
        'etc/openrc/conf.d/net-online'
        'etc/openrc/conf.d/network'
        'etc/openrc/conf.d/staticroute'
        'etc/openrc/conf.d/swap'
        'etc/openrc/conf.d/urandom')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenRC/openrc/archive/$pkgver.tar.gz"
		'openrc.logrotate'
		'quiet.patch'
		'revert1.patch'
		'revert2.patch')
install=openrc.install
#options=('debug' 'strip')
md5sums=('66346a6f436627a9ec1b1f32a460571f'
         '0d8a00d30812ec460ef8c32a96372c31'
         'c0ec3c5adb0e3ede01213727a26e5015'
         '2cc15fd6b1aacec9a55cf16e303916b5'
         '106730f674cda5638190814cca214edb')

_args=(
    SYSCONFDIR=/etc/openrc
    LIBEXECDIR=/usr/lib/openrc
    BRANDING="Cromnix"
	MKSYSVINIT=yes
    MKTERMCAP=ncurses
)

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
	patch -Np1 -i "${srcdir}"/quiet.patch
	patch -Np1 -i "${srcdir}"/revert1.patch
	patch -Np1 -i "${srcdir}"/revert2.patch
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make ${_args[@]}
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

	mkdir -pv "${pkgdir}"/usr/{bin,lib}
	ln -sv usr/bin "${pkgdir}/bin"
	ln -sv usr/bin "${pkgdir}/sbin"
	ln -sv usr/lib "${pkgdir}/lib"

    make DESTDIR="$pkgdir" ${_args[@]} install

    install -d ${pkgdir}/usr/lib/openrc/cache
    
    install -Dm644 ${srcdir}/openrc.logrotate ${pkgdir}/etc/logrotate.d/openrc

    install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    
	# remove sysvinit compat files
	rm "${pkgdir}"/usr/bin/{init,reboot,halt,poweroff,shutdown}
	
	# fix broken symlink
	ln -svf /usr/lib/openrc/bin/rc-sstat "${pkgdir}"/usr/bin/rc-sstat

    rm "${pkgdir}"/{bin,sbin,lib}
}
