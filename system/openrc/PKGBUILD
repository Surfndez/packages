# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=openrc
pkgver=0.28
pkgrel=4
pkgdesc="An init and service control software"
arch=('x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
groups=('cromnix-base')
depends=('kbd' 'procps-ng' 'psmisc' 'sed')
backup=('etc/openrc/rc.conf'
        'etc/openrc/conf.d/bootmisc'
        'etc/openrc/conf.d/consolefont'
        'etc/openrc/conf.d/devfs'
        'etc/openrc/conf.d/dmesg'
        'etc/openrc/conf.d/fsck'
        'etc/openrc/conf.d/hostname'
        'etc/openrc/conf.d/hwclock'
        'etc/openrc/conf.d/keymaps'
        'etc/openrc/conf.d/killprocs'
        'etc/openrc/conf.d/localmount'
        'etc/openrc/conf.d/modules'
        'etc/openrc/conf.d/mtab'
        'etc/openrc/conf.d/netmount'
        'etc/openrc/conf.d/net-online'
        'etc/openrc/conf.d/network'
        'etc/openrc/conf.d/staticroute'
        'etc/openrc/conf.d/swap'
        'etc/openrc/conf.d/urandom')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenRC/openrc/archive/$pkgver.tar.gz"
		'openrc.logrotate')
install=openrc.install
options=('debug' 'strip')
md5sums=('9e3172a3862a2264d61d541012fe7aad'
         '0d8a00d30812ec460ef8c32a96372c31')

_args=(
    SYSCONFDIR=/etc/openrc
    LIBEXECDIR=/usr/lib/openrc
    BRANDING="Cromnix"
	MKSYSVINIT=yes
    MKTERMCAP=ncurses
)

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make ${_args[@]}
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
	groups=('init' 'rc')
    cd "$srcdir/$pkgname-$pkgver"

	mkdir -pv "${pkgdir}"/usr/{bin,lib}
	ln -sv usr/bin "${pkgdir}/bin"
	ln -sv usr/bin "${pkgdir}/sbin"
	ln -sv usr/lib "${pkgdir}/lib"

    make DESTDIR="$pkgdir" ${_args[@]} install

    install -d ${pkgdir}/usr/lib/openrc/cache
    
    install -Dm644 ${srcdir}/openrc.logrotate ${pkgdir}/etc/logrotate.d/openrc

    install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    
	# remove sysvinit compat files
	rm "${pkgdir}"/usr/bin/{init,reboot,halt,poweroff,shutdown}
	
	# fix broken symlink
	ln -svf /usr/lib/openrc/bin/rc-sstat "${pkgdir}"/usr/bin/rc-sstat

    rm "${pkgdir}"/{bin,sbin,lib}
}
