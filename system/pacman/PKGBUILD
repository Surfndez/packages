# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=pacman
pkgname=('pacman' 'pacman-contrib')
pkgver=5.0.2
pkgrel=10
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
makedepends=('asciidoc' 'python2')
#checkdepends=('python2' 'fakechroot')
options=('strip')
source=("https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz"
        "pacman.conf"
        "makepkg.conf"
        'pacman.logrotate'
        'paccache.hook'
        'codename.patch'
        'epoch.patch')
md5sums=('f36f5e7e95a89436febe1bcca874fc33'
         '568acac0612bdc6329b2814212440f75'
         '5f978a84b691754a7a1588b8a5414b46'
         'bfed0ae8b6ec38fe6f5a1a6b1fcec871'
         'cd3b81be6dd7cfc4a441fd75b87f72dc'
         '680bde49286843272ccdfbea7c3da30b'
         '16bb3815c60745acaa2e5e2b8d6ec7d2')
         
# todo: missing depends for check

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    # allow codenames for cromnix
	patch -Np1 -i "${srcdir}"/codename.patch
	# use dash for epoch to work with sourceforge
	patch -Np1 -i "${srcdir}"/epoch.patch
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-doc \
        --with-scriptlet-shell=/usr/bin/bash \
        --with-ldconfig=/usr/bin/ldconfig
    make V=1
    make -C contrib
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package_pacman() {
	groups=('cromnix-base' 'cromnix-dev')
	depends=('perl' 'bash' 'glibc' 'libarchive' 'curl' 'gpgme' 'libxml2' 'cromnix-mirrorlist' 'cromnix-keyring')
	optdepends=('pacman-contrib: extra scripts for use with pacman'
				'bzip2: uncrompess bz2 source files'
				'xdelta3: delta updates for packages')
	backup=('etc/pacman.conf'
			'etc/makepkg.conf'
			'etc/logrotate.d/pacman')

    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    install -dm755 ${pkgdir}/etc/logrotate.d
    install -m644 ${srcdir}/makepkg.conf ${pkgdir}/etc
    install -m644 ${srcdir}/pacman.conf ${pkgdir}/etc
    sed -i "$pkgdir/etc/makepkg.conf" \
        -e "s|@CARCH[@]|x86_64|g" \
        -e "s|@CHOST[@]|x86_64-pc-linux-gnu|g" \
        -e "s|@CARCHFLAGS[@]|-march=x86-64|g"

    install -m644 ${srcdir}/pacman.logrotate ${pkgdir}/etc/logrotate.d/pacman

    install -dm755 "$pkgdir/usr/share/bash-completion/completions"
    mv "${pkgdir}"/etc/bash_completion.d/pacman "${pkgdir}"/usr/share/bash-completion/completions
    rm -rf "${pkgdir}"/etc/bash_completion.d

    for f in makepkg pacman-key; do
        ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
    done
}

package_pacman-contrib() {
	groups=('cromnix-base')
	depends=('pacman' 'bash' 'perl' 'curl')
	optdepends=('vim: for pacdiff')
	arch=('any')

    cd "$srcdir/$pkgbase-$pkgver"
    make DESTDIR="$pkgdir" -C contrib install
    
	install -Dm644 "${srcdir}"/paccache.hook "${pkgdir}"/usr/share/libalpm/hooks/paccache.hook

    rm -rf "${pkgdir}"/etc
}
