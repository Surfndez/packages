# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libarchive
pkgver=3.3.2
pkgrel=1
pkgdesc="The libarchive library provides a single interface for reading/writing various compression formats."
arch=('x86_64')
url="http://libarchive.org/"
license=('BSD')
depends=('acl' 'attr' 'bzip2' 'expat' 'openssl' 'xz' 'zlib')
makedepends=('acl-dev')
options=('strip' '!libtool')
source=("http://www.libarchive.org/downloads/$pkgname-$pkgver.tar.gz")
md5sums=('4583bd6b2ebf7e0e8963d90879eb1b27')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    autoreconf -fi
    ./configure --prefix=/usr \
        --disable-static \
        --without-nettle \
        --without-xml2
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/libarchive/COPYING"
}
