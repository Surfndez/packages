# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=bash
pkgname=('bash' 'libbash' 'bash-doc')
_basever=4.4
_patchver=012
pkgver=${_basever}.${_patchver}
pkgrel=1
pkgdesc='The Bourne-Again SHell'
arch=('x86_64')
url='http://www.gnu.org/software/bash/bash.html'
license=('GPL')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgbase}-${_basever}.tar.gz"
		'bashrc'
		'bash_logout')
# add patches to the source array
if [[ $((10#${_patchver})) -gt 0 ]]; then
	for (( _patch=1; _patch<=$((10#${_patchver})); _patch++ )); do
		source=(${source[@]} "https://ftp.gnu.org/gnu/${pkgname}/${pkgbase}-${_basever}-patches/bash${_basever//.}-$(printf "%03d" ${_patch})")
	done
fi
md5sums=('148888a7c95ac23705559b6f477dfe25'
         '50b4f0a6480454bb127365a89860098d'
         'ce8b85a173eb567c08420e0f8b982bc9'
         '817d01a6c0af6f79308a8b7b649e53d8'
         '765e14cff12c7284009772e8e24f2fe0'
         '49e7da93bf07f510a2eb6bb43ac3e5a2'
         '4557d674ab5831a5fa98052ab19edaf4'
         'cce96dd77cdd1d293beec10848f6cbb5'
         'd3379f8d8abce5c6ee338f931ad008d5'
         'ec38c76ca439ca7f9c178e9baede84fc'
         'e0ba18c1e3b94f905da9b5bf9d38b58b'
         'e952d4f44e612048930c559d90eb99bb'
         '57b5b35955d68f9a09dbef6b86d2c782'
         'cc896e1fa696b93ded568e557e2392d5'
         'fa47fbfa56fb7e9e5367f19a9df5fc9e')

prepare() {
	cd ${srcdir}/${pkgbase}-${_basever}
	if [[ $((10#${_patchver})) -gt 0 ]]; then
	for (( _patch=1; _patch<=$((10#${_patchver})); _patch++ )); do
			patch -Np0 -i "${srcdir}"/bash${_basever//.}-$(printf "%03d" ${_patch})
		done
	fi
}

build() {
	cd ${srcdir}/${pkgbase}-${_basever}

	_bashconfig=(-DSYS_BASHRC=\'\"/etc/bash/bashrc\"\'
		     -DSYS_BASH_LOGOUT=\'\"/etc/bash/bash_logout\"\'
		     -DNON_INTERACTIVE_LOGIN_SHELLS)
	export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

	./configure --prefix=/usr \
		--docdir=/usr/share/doc/${pkgbase}-${_basever} \
		--without-bash-malloc \
		--with-installed-readline
	make

	sed -e "s#${srcdir}/${pkgbase}-${_basever}#./#" -i examples/loadables/Makefile.inc
}

check() {
	make -C ${pkgbase}-${_basever} check
}

package_bash() {
	groups=('cromnix-base')
	provides=('sh')
	depends=('libbash' 'readline' 'glibc' 'ncurses')
	optdepends=('bash-doc: bash documentation')
	cd ${srcdir}/${pkgbase}-${_basever}
	make DESTDIR=${pkgdir} install

	rm -rf ${pkgdir}/usr/{lib,include}
	rm -rf ${pkgdir}/usr/share/{doc,locale}

	install -dm755 ${pkgdir}/etc/bash/bashrc.d
	install -Dm644 ${srcdir}/bashrc ${pkgdir}/etc/bash/bashrc
	install -Dm644 ${srcdir}/bash_logout ${pkgdir}/etc/bash/bash_logout
}

package_libbash() {
	cd ${srcdir}/${pkgbase}-${_basever}
	make DESTDIR=${pkgdir} install

	rm -rf ${pkgdir}/usr/bin
	rm -rf ${pkgdir}/usr/share/{doc,man,info}
}

package_bash-doc() {
	arch=('any')
	cd ${srcdir}/${pkgbase}-${_basever}
	make DESTDIR=${pkgdir} install

	rm -rf ${pkgdir}/usr/{bin,include,lib}
	rm -rf ${pkgdir}/usr/share/{info,locale,man}
}
