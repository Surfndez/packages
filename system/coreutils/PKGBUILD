# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=coreutils
pkgver=8.28
pkgrel=1
pkgdesc='Contains utilities for showing and setting the basic system characteristics'
arch=('x86_64')
url='http://www.gnu.org/software/coreutils'
license=('GPL3')
groups=('cromnix-base')
depends=('glibc' 'libacl' 'libattr' 'gmp' 'libcap' 'openssl')
makedepends=('acl-dev')
source=("ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('e7cb20d0572cc40d9f47ede6454406d1')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-no-install-program=kill,uptime
	make
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make check
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}
