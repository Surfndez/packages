# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=glibc
pkgver=2.26
pkgrel=1
pkgdesc='The main C library'
arch=('x86_64')
url='https://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
groups=('cromnix-base')
depends=('linux-api-headers>=4.12' 'cromnix' 'tzdata')
options=('!strip' 'staticlibs' '!emptydirs')
install=glibc.install
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"
		"${pkgname}-fhs.patch")
md5sums=('102f637c3812f81111f48f2427611be1'
         '9a5997c3452909b1769918c759eff8a2')
         
# todo: rebuild with gd

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -Np1 -i ${srcdir}/${pkgname}-fhs.patch

	mkdir -pv ${srcdir}/${pkgname}-${pkgver}/build
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}/build

	echo "slibdir=/usr/lib" >> configparms
	echo "rtlddir=/usr/lib" >> configparms
	echo "sbindir=/usr/bin" >> configparms
	echo "rootsbindir=/usr/bin" >> configparms

	CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

	../configure --prefix=/usr \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--with-headers=/usr/include \
		--enable-add-ons \
		--enable-bind-now \
		--enable-lock-elision \
		--enable-multi-arch \
		--disable-profile \
		--enable-kernel=2.6.32 \
		--enable-obsolete-rpc \
		--enable-obsolete-nsl \
		--enable-stack-protector=strong \
		--enable-stackguard-randomization \
		--disable-werror

	echo "build-programs=no" >> configparms
	make

	sed -i "/build-programs=/s#no#yes#" configparms
	echo "CC += -fstack-protector-strong" >> configparms
	echo "CXX += -fstack-protector-strong" >> configparms

	make
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}/build
	sed -i '/FORTIFY/d' configparms
	make check || true
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}/build

	install -dm755 ${pkgdir}/etc
	touch ${pkgdir}/etc/ld.so.conf
	make install_root=${pkgdir} install

	rm -f ${pkgdir}/etc/ld.so.{cache,conf}

	cp -v ../nscd/nscd.conf ${pkgdir}/etc/nscd.conf
	mkdir -pv ${pkgdir}/var/cache/nscd

	make localedata/install-locales install_root=${pkgdir}
}
