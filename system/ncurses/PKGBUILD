# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=ncurses
pkgver=6.0
pkgrel=1
pkgdesc="The Ncurses package contains libraries for terminal-independent handling of character screens."
arch=('x86_64')
url="https://www.gnu.org/software/ncurses/"
license=('GPL')
depends=('glibc' 'gcc-libs')
source=("http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('ee13d052e1ead260d7c28071f46eefb1')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    sed -i '/LIBTOOL_INSTALL/d' c++/Makefile.in
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --with-shared \
        --without-debug \
        --without-normal \
        --enable-pc-files \
        --enable-widec \
        --libdir=/usr/lib \
        --with-pkg-config-libdir=/usr/lib/pkgconfig
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    ln -sfv $(readlink $pkgdir/usr/lib/libncursesw.so) ${pkgdir}/usr/lib/libncursesw.so

    mkdir -pv ${pkgdir}/usr/lib/pkgconfig
    for lib in ncurses form panel menu ; do
        rm -vf ${pkgdir}/usr/lib/lib${lib}.so
        echo "INPUT(-l${lib}w)" > ${pkgdir}/usr/lib/lib${lib}.so
        ln -sfv ${lib}w.pc ${pkgdir}/usr/lib/pkgconfig/${lib}.pc
    done

    rm -vf ${pkgdir}/usr/lib/libcursesw.so
    echo "INPUT(-lncursesw)" > ${pkgdir}/usr/lib/libcursesw.so
    ln -sfv libncurses.so ${pkgdir}/usr/lib/libcurses.so

    mkdir -pv ${pkgdir}/usr/share/doc/ncurses-6.0
    cp -v -R doc/* ${pkgdir}/usr/share/doc/ncurses-6.0

    #ncurses 5 links
    #make distclean
    #./configure --prefix=/usr    \
    #        --with-shared    \
    #        --without-normal \
    #        --without-debug  \
    #        --without-cxx-binding \
    #        --with-abi-version=5
    #make sources libs
    #cp -av lib/lib*.so.5* /usr/lib
}
