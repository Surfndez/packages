# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=expat
pkgver=2.2.3
pkgrel=1
pkgdesc="The Expat package contains a stream oriented C library for parsing XML."
arch=('x86_64')
url="http://expat.sourceforge.net/"
license=('custom')
depends=('glibc')
source=("http://prdownloads.sourceforge.net/expat/$pkgname-$pkgver.tar.bz2")
md5sums=('f053af63ef5f39bd9b78d01fbc203334')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr \
        --disable-static
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING

    install -v -dm755 ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
    install -v -m644 doc/*.{html,png,css} ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
}
