# Maintainer: Chris Cromer <cromer@cromnix.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc->libtool->valgrind

pkgname=linux-api-headers
pkgver=4.12.10
pkgrel=1
pkgdesc='Kernel headers sanitized for use in userspace'
arch=('any')
url='http://www.kernel.org/'
license=('GPL2')
source=("http://www.kernel.org/pub/linux/kernel/v4.x/linux-${pkgver}.tar.xz")
md5sums=('8cfe8bd3bd00c0bb10171441e1a9c99e')

build() {
	cd "${srcdir}/linux-${pkgver}"
	make mrproper
}

package() {
	cd "${srcdir}/linux-${pkgver}"
	make INSTALL_HDR_PATH="${pkgdir}/usr" headers_install
	find "${pkgdir}" \( -name .install -o -name ..install.cmd \) -delete
}
