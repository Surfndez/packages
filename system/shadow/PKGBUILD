# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=shadow
pkgver=4.5
pkgrel=1
pkgdesc="The Shadow package contains programs for handling passwords in a secure way"
arch=('x86_64')
url="https://github.com/shadow-maint/shadow/"
license=('BSD')
groups=('cromnix-base')
depends=('libbash' 'libacl')
makedepends=('acl-dev')
backup=('etc/login.defs'
        'etc/default/useradd')
options=('strip')
source=("https://github.com/shadow-maint/shadow/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('c350da50c2120de6bb29177699d89fe3')

# todo: rebuild with cracklib

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    sed -i 's/groups$(EXEEXT) //' src/Makefile.in
    find man -name Makefile.in -exec sed -i 's/groups\.1 / /'   {} \;
    find man -name Makefile.in -exec sed -i 's/getspnam\.3 / /' {} \;
    find man -name Makefile.in -exec sed -i 's/passwd\.5 / /'   {} \;

    sed -i -e 's@#ENCRYPT_METHOD DES@ENCRYPT_METHOD SHA512@' \
        -e 's@/var/spool/mail@/var/mail@' etc/login.defs

    sed -i 's/1000/100/' etc/useradd

	sed -i -e '47 d' -e '60,65 d' libmisc/myname.c
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure \
		--prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-group-name-max-length=32
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    mv ${pkgdir}/usr/sbin/* ${pkgdir}/usr/bin
    rm -rf ${pkgdir}/usr/sbin
}
