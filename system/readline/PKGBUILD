# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=readline
pkgver=7.0
pkgrel=1
pkgdesc="The Readline package is a set of libraries that offers command-line editing and history capabilities."
arch=('x86_64')
url="http://cnswww.cns.cwru.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('glibc' 'ncurses')
options=('!emptydirs')
source=("http://ftp.gnu.org/gnu/readline/$pkgname-$pkgver.tar.gz")
md5sums=('205b03a87fc83dab653b628c59b9fc91')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"

    sed -i '/MV.*old/d' Makefile.in
    sed -i '/{OLDSUFF}/c:' support/shlib-install
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr \
        --disable-static \
        --docdir=/usr/share/doc/${pkgname}-${pkgver}
    make SHLIB_LIBS=-lncurses
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" SHLIB_LIBS=-lncurses install

    ln -sfv $(readlink ${pkgdir}/usr/lib/libreadline.so) ${pkgdir}/usr/lib/libreadline.so
    ln -sfv $(readlink ${pkgdir}/usr/lib/libhistory.so ) ${pkgdir}/usr/lib/libhistory.so

    install -v -m644 doc/*.{ps,pdf,html,dvi} ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
}
