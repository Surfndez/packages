# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=gmp
pkgver=6.1.2
pkgrel=1
pkgdesc="Math libraries for arbitrary precision arithmetic"
arch=('x86_64')
url="http://gmplib.org/"
license=('LGPL3' 'GPL')
depends=('gcc-libs' 'sh')
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('f58fa8001d60c4c77595fbbb62b63c1d')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr \
        --enable-cxx \
        --disable-static \
        --docdir=/usr/share/doc/${pkgname}-${pkgver}
    make
    make html
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check 2>&1 | tee gmp-check-log
    awk '/# PASS:/{total+=$3} ; END{print total}' gmp-check-log
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    make DESTDIR="$pkgdir" install-html
}
