# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=bc
pkgver=1.07.1
pkgrel=1
pkgdesc='Arbitrary precision numeric processing language'
arch=('x86_64')
url='http://www.gnu.org/software/bc/'
license=('GPL')
depends=('readline')
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('cda93857418655ea43590736fc3ca9fc')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	# Replace ed with sed
	cat > bc/fix-libmath_h << "EOF"
#! /bin/bash
sed -e '1   s/^/{"/' \
    -e     's/$/",/' \
    -e '2,$ s/^/"/'  \
    -e   '$ d'       \
    -i libmath.h

sed -e '$ s/$/0}/' \
    -i libmath.h
EOF
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--with-readline \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	echo "quit" | ./bc/bc -l Test/checklib.b
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
