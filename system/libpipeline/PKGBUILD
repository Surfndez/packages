#Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libpipeline
pkgver=1.4.2
pkgrel=1
pkgdesc="The Libpipeline package contains a library for manipulating pipelines of subprocesses in a flexible and convenient way."
arch=('x86_64')
url="http://libpipeline.nongnu.org/"
license=('GPL')
depends=('glibc')
source=("http://download.savannah.gnu.org/releases/libpipeline/$pkgname-$pkgver.tar.gz")
md5sums=('d5c80387eb9c9e5d089da2a06e8a6b12')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
