# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=texinfo
pkgver=6.4
pkgrel=1
pkgdesc="The Texinfo package contains programs for reading, writing, and converting info pages."
arch=('x86_64')
url="http://www.gnu.org/software/texinfo/"
license=('GPL3')
groups=('cromnix-dev')
depends=('ncurses' 'gzip' 'perl' 'sh')
source=("http://ftp.gnu.org/gnu/texinfo/$pkgname-$pkgver.tar.xz")
md5sums=('2a676c8339efe6ddea0f1cb52e626d15')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr \
        --disable-static
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    make DESTDIR="$pkgdir" TEXMF=/usr/share/texmf install-tex

    pushd ${pkgdir}/usr/share/info
    rm -v dir
    for f in *
        do install-info $f dir 2>/dev/null
    done
    popd
}
