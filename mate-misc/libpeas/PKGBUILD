# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libpeas
pkgver=1.20.0
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='libpeas is a GObject based plugins engine, and is targeted at giving every application the chance to assume its own extensibility'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/Libpeas'
license=('GPL2')
depends=('gtk3' 'gobject-introspection')
# todo: 'gobject-introspection-runtime'
makedepends=('gtk-doc' 'intltool' 'gobject-introspection' 'atk-dev' 'at-spi2-atk-dev')
# 'python-gobject' 'python2-gobject' 'glade'
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('67a698cd64c9d9efc1289f68420883b1')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--enable-gtk-doc \
		--disable-static
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
