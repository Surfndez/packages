# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libsoup
pkgver=2.58.1
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='The libsoup is a HTTP client/server library for GNOME'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/libsoup'
license=('LGPL')
depends=('glib' 'libxml2' 'glib-networking' 'sqlite')
makedepends=('intltool' 'python' 'vala' 'gtk-doc')
#checkdepends=(apache php-apache)
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('91d7a6bf8785d31f4b154a7612e53e62')

# todo: rebuild with depends

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--enable-gtk-doc \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
