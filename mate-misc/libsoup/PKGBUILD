# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libsoup
pkgver=2.58.2
_pkgver=${pkgver%.*}
pkgrel=2
pkgdesc='The libsoup is a HTTP client/server library for GNOME'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/libsoup'
license=('LGPL')
depends=('glib' 'libxml2' 'glib-networking' 'sqlite')
makedepends=('intltool' 'python' 'vala' 'gtk-doc' 'gobject-introspection')
#checkdepends=(apache php-apache)
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('eb33adb459c2283efc5c7d09ccdbbcfc')

# todo: rebuild with depends

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--enable-gtk-doc \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
