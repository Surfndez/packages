# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=glade
pkgver=3.20.1
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='User Interface Builder for GTK+ applications'
arch=('x86_64')
url='https://glade.gnome.org/'
license=('GPL' 'LGPL')
depends=('gtk3' 'libxml2')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'python-gobject' 'itstool' 'docbook-xsl' 'gnome-common' 'atk-dev' 'at-spi2-atk-dev')
optdepends=('python: Python widgets support'
			'devhelp: help browser')
source=("https://download.gnome.org/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('60f01ccc4e9a4e5de5a5402d10495dc4')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--enable-gtk-doc
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
