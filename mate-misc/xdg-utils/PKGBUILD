# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=xdg-utils
pkgver=1.1.2
pkgrel=1
pkgdesc='xdg-utils is a a set of command line tools that assist applications with a variety of desktop integration tasks'
arch=('any')
url='https://www.freedesktop.org/wiki/Software/xdg-utils/'
license=('MIT')
depends=('sh' 'xset')
makedepends=('docbook-xsl' 'lynx' 'xmlto')
optdepends=('kde-cli-tools: for KDE Plasma5 support in xdg-open'
            'exo: for Xfce support in xdg-open'
            'xprop: for Xfce support in xdg-open'
            'pcmanfm: for LXDE support in xdg-open'
            'perl-file-mimeinfo: for generic support in xdg-open'
            'perl-net-dbus: Perl extension to dbus used in xdg-screensaver'
            'perl-x11-protocol: Perl X11 protocol used in xdg-screensaver')
source=("https://portland.freedesktop.org/download/${pkgname}-${pkgver}.tar.gz")
md5sums=('361e75eb76c94d19f6f4f330d8ee626b')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--mandir=/usr/share/man
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make -k test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
