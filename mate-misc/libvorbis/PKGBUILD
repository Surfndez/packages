# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libvorbis
pkgver=1.3.5
pkgrel=1
pkgdesc='The libvorbis package contains a general purpose audio and music encoding format'
arch=('x86_64')
url='http://www.xiph.org/vorbis/'
license=('BSD')
depends=('libogg')
source=("http://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.xz")
md5sums=('28cb28097c07a735d6af56e598e1c90f')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i '/components.png \\/{n;d}' doc/Makefile.in
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--disable-static
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make LIBS=-lm check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -v -m644 doc/Vorbis* "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
}
