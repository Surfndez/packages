# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=at-spi2-core
pkgver=2.26.0
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='The At-Spi2 Core package is a part of the GNOME Accessibility Project'
arch=('x86_64')
url='http://www.gnome.org'
license=('GPL2')
depends=('dbus' 'glib' 'libxtst')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('ef3de25da46da8f650915205eb7e1a33')

# todo: there is a systemd service included with no openrc alternative

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--libdir=/usr/lib \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/at-spi2-core \
		--enable-gtk-doc
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# remove the systemd unit
	rm -rf ${pkgdir}/usr/lib/systemd
}
