# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=atk
pkgname=('libatk' 'atk-dev' 'atk-doc')
pkgver=2.26.0
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='ATK provides the set of accessibility interfaces that are implemented by other toolkits and applications'
arch=('x86_64')
url='https://developer.gnome.org/atk/'
license=('LGPL')
makedepends=('glib' 'gobject-introspection' 'gtk-doc')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/${_pkgver}/${pkgbase}-${pkgver}.tar.xz")
md5sums=('ee9c329784dead6e386e8b2e0d4d8d6f')

# todo: split gir package

prepare() {
	if [ -d "${srcdir}/install" ]; then
		rm -rf "${srcdir}/install"
	fi
	mkdir -p "${srcdir}/install"
}

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	./configure --prefix=/usr \
		--enable-gtk-doc
	make

	# split package
	make DESTDIR="${srcdir}/install" install
}

package_libatk() {
	depends=('glib')

	mkdir -p "${pkgdir}/usr/share"
	cp -vr "${srcdir}/install/usr/lib" "${pkgdir}/usr/lib"
	#rm -rf "${pkgdir}/usr/lib/girepository-1.0"
	cp -vr "${srcdir}/install/usr/share/locale" "${pkgdir}/usr/share/locale"
}

package_atk-dev() {
	arch=('any')

	mkdir -p "${pkgdir}"/usr/share
	cp -vr "${srcdir}/install/usr/include" "${pkgdir}/usr/include"
	cp -vr "${srcdir}/install/usr/share/gir-1.0" "${pkgdir}/usr/share/gir-1.0"
}

package_atk-gir() {	
	mkdir -p "${pkgdir}/usr/lib"
	cp -vr "${srcdir}/install/usr/lib/girepository-1.0" "${pkgdir}/usr/lib"
}

package_atk-doc() {
	arch=('any')

	mkdir -p "${pkgdir}/usr/share"
	cp -vr "${srcdir}/install/usr/share/gtk-doc" "${pkgdir}/usr/share/gtk-doc"
}
