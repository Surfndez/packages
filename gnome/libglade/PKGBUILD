# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libglade
pkgver=2.6.4
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='Allows you to load glade interface files in a program at runtime'
arch=('x86_64')
url='https://www.gnome.org'
license=('LGPL')
depends=('gtk2' 'libxml2')
makedepends=('python2' 'pkg-config' 'gtk-doc' 'atk-dev')
optdepends=('python2: libglade-convert script')
install=libglade.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('d1776b40f4e166b5e9c107f1c8fe4139')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i '/DG_DISABLE_DEPRECATED/d' glade/Makefile.in
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
		--disable-static \
		--localstatedir=/var \
		--sysconfdir=/etc \
		--enable-gtk-doc
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -m755 libglade-convert "${pkgdir}"/usr/bin/
}
