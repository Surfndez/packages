# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=librsvg
pkgver=2.40.18
_pkgver=${pkgver%.*}
pkgrel=2
pkgdesc='The librsvg package contains a library and tools used to manipulate, convert and view Scalable Vector Graphic (SVG) images'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/LibRsvg'
license=('LGPL')
depends=('gdk-pixbuf' 'pango' 'libcroco' 'gtk3' 'libatk')
makedepends=('intltool' 'python2' 'atk-dev' 'gobject-introspection' 'vala' 'gtk-doc')
options=('!emptydirs')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('eaa5c8a8bbe2600ab5194c0d3b1b621b')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--disable-static \
		--enable-vala \
		--enable-gtk-doc
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
