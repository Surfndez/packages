# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=cryptsetup
pkgver=1.7.5
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='cryptsetup is used to set up transparent encryption of block devices using the kernel crypto API'
arch=('x86_64')
url='https://gitlab.com/cryptsetup/cryptsetup/'
license=('GPL')
groups=('cromnix-base')
depends=('device-mapper' 'libgcrypt' 'popt' 'libutil-linux')
makedepends=('util-linux')
options=('!emptydirs')
source=("https://www.kernel.org/pub/linux/utils/${pkgname}/v${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('d2d668223e795dcf750da44dc3e7076b')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--disable-static \
		--enable-cryptsetup-reencrypt
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
