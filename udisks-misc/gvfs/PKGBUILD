# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=gvfs
pkgver=1.32.1
_pkgver=${pkgver%.*}
pkgrel=2
pkgdesc='The Gvfs package is a userspace virtual filesystem designed to work with the I/O abstractions of GLib''s GIO library'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/gvfs'
license=('LGPL')
depends=('dconf' 'libarchive' 'libsoup' 'udisks' 'libgudev' 'psmisc')
makedepends=('dbus' 'intltool' 'docbook-xsl' 'gtk3' 'gtk-doc' 'python' 'atk-dev')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz"
		'gvfsd.hook')
md5sums=('cbe766b46f324e17d7abcfb4a89a1684'
         '1ea77962914c023799f1aa9e729ca29c')

# todo: rebuild with depends

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-gphoto2 \
		--libexecdir=/usr/lib/gvfs \
		--disable-gcr
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make -k check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../gvfsd.hook "${pkgdir}"/usr/share/libalpm/hooks/gvfsd.hook
	install -d -o root -g 102 -m 750 "${pkgdir}"/usr/share/polkit-1/rules.d
}
