# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=cantarell-fonts
pkgver=0.0.25
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='Cantarell font family'
arch=('any')
url='https://git.gnome.org/browse/cantarell-fonts'
license=('custom:SIL')
makedepends=('fontconfig')
source=("https://download.gnome.org/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('8fa68460bb292b9c9441af10f99d99d7')

build() {
	cd "${srcdir}"/${pkgname}-${pkgver}
	./configure --prefix=/usr \
		--with-configdir=/etc/fonts/conf.avail
	make
}

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install

	mkdir "${pkgdir}"/etc/fonts/conf.d
	ln -srt "${pkgdir}"/etc/fonts/conf.d "${pkgdir}"/etc/fonts/conf.avail/*

	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
