#Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=lxdm
pkgver=0.5.3
pkgrel=1
pkgdesc='The LXDM is a lightweight Display Manager for the LXDE desktop. It can also be used as an alternative to other Display Managers such as GNOME''s GDM or LightDM'
arch=('x86_64')
url='https://www.lxde.org/'
license=('GPL2')
groups=('lxde')
depends=('gtk2' 'xorg-server')
makedepends=('intltool' 'atk-dev' 'iso-codes')
optdepends=('iso-codes: show language names in language chooser'
			'librsvg: display the default background')
source=("http://downloads.sourceforge.net/lxdm/${pkgname}-${pkgver}.tar.xz"
		'Xsession'
		'openrc-shutdown.patch')
md5sums=('061caae432634e6db38bbdc84bc6ffa0'
         'e4cd37456143b83da8796a67eab54b6f'
         '4fc2ad7a8c03a8bd08df0bb874f4b86e')

# todo: depends pam

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -Np1 -i "${srcdir}/openrc-shutdown.patch"
	sed -i 's:sysconfig/il8n:profile.d/il8n.sh:g' data/lxdm.in
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/lxdm \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--without-pam \
		--with-systemdsystemunitdir=no
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -v -m0755 "${srcdir}"/Xsession "${pkgdir}"/etc/lxdm/Xsession
}
