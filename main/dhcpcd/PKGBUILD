# Maintainer: Chris Cromer <chris@cromer.cl>

pkgbase=dhcpcd
pkgname=('dhcpcd' 'dhcpcd-openrc')
pkgver=6.11.5
pkgrel=2
pkgdesc='dhcpcd is an implementation of the DHCP client specified in RFC2131'
arch=('x86_64')
url='http://roy.marples.name/projects/dhcpcd/index'
license=('BSD')
source=("http://roy.marples.name/downloads/${pkgbase}/${pkgbase}-${pkgver}.tar.xz"
		"https://gitweb.gentoo.org/repo/gentoo.git/plain/net-misc/${pkgbase}/files/${pkgbase}.initd"
		'dhcpcd-openrc-install.hook'
		'dhcpcd-openrc-upgrade.hook'
		'dhcpcd-openrc.script')
md5sums=('2465624b62c1154f0e89dc69c42c849b'
         '5cd5809d11b77b30f21df8418729d70b'
         '4262225f6016a1d87c11997a372b7965'
         '87a3148d4a8dbf148d56e4df5074589f'
         '32b72d1db8e77f446151d6023e30b092')

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	./configure \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib/dhcpcd \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--dbdir=/var/lib/dhcpcd
	make
}

package_dhcpcd() {
	depends=('dhcpcd-init')
	backup=('etc/dhcpcd.conf')
	cd ${srcdir}/${pkgbase}-${pkgver}
	make DESTDIR=${pkgdir} install
}

package_dhcpcd-openrc() {
	pkgdesc="OpenRC init scripts for ${pkgbase}"
	arch=('any')
	depends=('openrc')
	provides=('dhcpcd-init')
	install=dhcpcd-openrc.install
	install -v -m755 -d ${pkgdir}/etc/openrc/init.d
	install -v -m755 ${srcdir}/dhcpcd.initd ${pkgdir}/etc/openrc/init.d/dhcpcd
	
	install -Dm644 ${srcdir}/dhcpcd-openrc-install.hook ${pkgdir}/usr/share/libalpm/hooks/dhcpcd-openrc-install.hook
	install -Dm644 ${srcdir}/dhcpcd-openrc-upgrade.hook ${pkgdir}/usr/share/libalpm/hooks/dhcpcd-openrc-upgrade.hook
	install -Dm755 ${srcdir}/dhcpcd-openrc.script ${pkgdir}/usr/share/libalpm/scripts/dhcpcd-openrc
}
