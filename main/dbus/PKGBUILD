# Maintainer: Chris Cromer <chris@cromer.cl>

pkgbase=dbus
pkgname=('dbus' 'dbus-openrc')
pkgver=1.10.18
pkgrel=3
pkgdesc='D-Bus is a message bus system, a simple way for applications to talk to one another'
arch=('x86_64')
url='https://wiki.freedesktop.org/www/Software/dbus/'
license=('GPL' 'custom')
makedepends=('xmlto' 'docbook-xsl' 'python' 'libx11' 'dbus')
source=("http://dbus.freedesktop.org/releases/${pkgbase}/${pkgbase}-${pkgver}.tar.gz"
		'https://gitweb.gentoo.org/repo/gentoo.git/plain/sys-apps/dbus/files/dbus.initd-r1'
		'https://raw.githubusercontent.com/cromnix/openrc-services/master/svc-openrc-install.hook'
		'https://raw.githubusercontent.com/cromnix/openrc-services/master/svc-openrc-upgrade.hook'
		'https://raw.githubusercontent.com/cromnix/openrc-services/master/svc-openrc-remove.hook'
		'https://raw.githubusercontent.com/cromnix/openrc-services/master/svc-openrc.script'
		'30-dbus.sh')
md5sums=('1209c455598165a0c5263d4201894179'
         '779e0690de06451c05371c4fd47c67c2'
         'b40e4171b91d6c488d4f349083460383'
         '65041d59dc10dfb0de74637675e51ce7'
         '9e2098cc56d38fc6bcd04ba3aa8edb96'
         '964664ce8a9d83544e476c76ca9adc36'
         '9e7e4011d29c6eb8b865e603ddc4603e')

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	./configure --prefix=/usr \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib/dbus-1.0 \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-doxygen-docs \
		--with-dbus-user=dbus \
		--disable-xml-docs \
		--disable-static \
		--disable-systemd \
		--enable-inotify \
		--disable-verbose-mode \
		--disable-static \
		--disable-tests \
		--disable-asserts \
		--disable-libaudit \
		--disable-ducktype-docs \
		--without-systemdsystemunitdir \
		--with-console-auth-dir=/run/console/ \
		--with-system-pid-file=/run/dbus/pid \
		--with-system-socket=/run/dbus/system_bus_socket \
		--enable-x11-autolaunch \
		--docdir=/usr/share/doc/${pkgbase}-${pkgver}
	make
}

check() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	make check
}

package_dbus() {
	depends=('dbus-init' 'expat')
	optdepends=('libx11: dbus-launch support')
	install=dbus.install
	cd ${srcdir}/${pkgbase}-${pkgver}
	make DESTDIR=${pkgdir} install
	chown -v root:dbus ${pkgdir}/usr/lib/dbus-1.0/dbus-daemon-launch-helper
	chmod -v 4750 ${pkgdir}/usr/lib/dbus-1.0/dbus-daemon-launch-helper

	rm -r ${pkgdir}/var/run
	
	install -v -m755 -d ${pkgdir}/etc/X11/xinit/xinitrc.d
	install -m755 ${srcdir}/30-dbus.sh ${pkgdir}/etc/X11/xinit/xinitrc.d/30-dbus.sh
}

package_dbus-openrc() {
	pkgdesc="OpenRC init scripts for dbus"
	arch=('any')
	depends=('openrc')
	provides=('dbus-init')
	install -v -m755 -d ${pkgdir}/etc/openrc/init.d
	install -v -m755 ${srcdir}/dbus.initd-r1 ${pkgdir}/etc/openrc/init.d/dbus

	sed -e 's|dbus.pid|dbus/pid|g' -i ${pkgdir}/etc/openrc/init.d/dbus
	
	install -Dm644 "${srcdir}"/svc-openrc-install.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-install.hook
	install -Dm644 "${srcdir}"/svc-openrc-upgrade.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-upgrade.hook
	install -Dm644 "${srcdir}"/svc-openrc-remove.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-remove.hook
	install -Dm755 "${srcdir}"/svc-openrc.script "${pkgdir}"/usr/share/libalpm/scripts/${pkgbase}-openrc
	
	sed -e "s/@svc@/${pkgbase}/" -i "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-{install,upgrade,remove}.hook
	sed -e "s/@svc_enable@/'${pkgbase}'/" \
		-e "s/@svc_run@/'${pkgbase}'/" \
		-e "s/@svc_remove@/'${pkgbase}'/" \
		-e "s/@svlevel@/default/" \
		-i "${pkgdir}"/usr/share/libalpm/scripts/${pkgbase}-openrc
}
