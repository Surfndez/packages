# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=polkit
pkgver=r917.3ae3092
pkgrel=1
pkgdesc='Polkit is a toolkit for defining and handling authorizations. It is used for allowing unprivileged processes to communicate with privileged processes'
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/polkit/'
license=('LGPL')
depends=('glib' 'expat' 'js')
makedepends=('git' 'intltool' 'icu' 'gobject-introspection' 'gtk-doc')
install=polkit.install
source=("git+https://anongit.freedesktop.org/git/polkit.git"
		'js38.patch')
md5sums=('SKIP'
         'a6e4131614fa9536934d4e7749c1699a')

pkgver() {
	cd "$pkgbase"
	#printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)" "$(date +%Y%m%d)"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/${pkgname}"
	# patch to use js38 with polkit
    patch -Np1 -i ${srcdir}/js38.patch
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}"
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/polkit-1 \
		--disable-static \
		--enable-libsystemd-login=no \
		--disable-systemd \
		--enable-man-pages \
		--enable-introspection \
		--disable-examples \
		--with-authfw=shadow \
		--with-os-type=gentoo \
		--enable-gtk-doc
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd "${srcdir}/${pkgname}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install
	chown root:27 "${pkgdir}"/{etc,usr/share}/polkit-1/rules.d
	chmod 750 "${pkgdir}"/{etc,usr/share}/polkit-1/rules.d
}
