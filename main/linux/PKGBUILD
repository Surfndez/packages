# Maintainer: Chris Cromer <chris@cromer.cl>

pkgbase=linux
pkgname=('linux' 'linux-doc')
pkgver=4.9.31
pkgrel=1
pkgdesc="This package contain the long term linux kernel"
arch=('x86_64')
url="http://www.kernel.org/"
license=('GPL2')
makedepends=('docbook-xsl' 'kmod' 'inetutils' 'bc')
options=('!strip')
source=("https://www.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz"
        "config"
        "usb.conf"
        "linux-lib-path.patch")
md5sums=('6f58df83cfb2f0db2bdce3f132870516'
         'dbbb15ac2d03579f4c0eb408d653e87a'
         '30994cd9cba3aa218b3afa33d34eb649'
         '9d34da065c7c15ab5aa73fa4b44f4c21')

prepare() {
    cd "$srcdir/linux-$pkgver"
    make mrproper

    cp -v ${srcdir}/config .config
    #make olddefconfig
	#make menuconfig
    patch -Np1 -i ${srcdir}/linux-lib-path.patch

    sed -i '2iexit 0' scripts/depmod.sh

	sed -ri "s|^(EXTRAVERSION =).*|\1 -${pkgrel}|" Makefile
}

build() {
    cd "$srcdir/linux-$pkgver"
    make LOCALVERSION= bzImage modules
}

package_linux() {
	groups=('kernel')
	optdepends=('linux-doc: linux documentation')

    cd "$srcdir/linux-$pkgver"

	_kernelversion=$(make LOCALVERSION= kernelrelease)
	sed -e "s|@VERSION@|${_kernelversion}|g" \
		"${startdir}/linux.in" > "${startdir}/linux.install"
		true && install=linux.install

    ln -sv ${pkgdir}/usr/lib ${pkgdir}/lib

    make LOCALVERSION= INSTALL_MOD_PATH="${pkgdir}" modules_install

    mkdir -pv ${pkgdir}/boot
    cp -v arch/x86/boot/bzImage ${pkgdir}/boot/vmlinuz-${pkgver}-${pkgrel}-cromnix
    cp -v System.map ${pkgdir}/boot/System.map-${pkgver}-${pkgrel}-cromnix
    cp -v .config ${pkgdir}/boot/config-${pkgver}-${pkgrel}-cromnix

    install -vd -m755 ${pkgdir}/etc/modprobe.d
    install -m644 ${srcdir}/usb.conf ${pkgdir}/etc/modprobe.d/

	#rm -rf "${pkgdir}/usr/lib/firmware"

    cd ${pkgdir}/boot

    # Remove this in the future at some point, but it's useful for now
    ln -sv vmlinuz-${pkgver}-${pkgrel}-cromnix ${pkgdir}/boot/vmlinuz

    depmod -b "${pkgdir}" -F "${pkgdir}/boot/System.map-${pkgver}-${pkgrel}-cromnix" "${_kernelversion}"

    rm -v ${pkgdir}/lib
}

package_linux-doc() {
	groups=('kernel-doc')
	arch=('any')

    cd "$srcdir/linux-$pkgver"

	mkdir -pv ${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}
    cp -r Documentation/* ${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}
}
