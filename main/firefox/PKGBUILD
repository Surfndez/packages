# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=firefox
pkgver=53.0.3
pkgrel=3
pkgdesc='A modern, fast, and secure web browser'
arch=('x86_64')
url='https://www.mozilla.org/firefox/'
license=('MPL' 'GPL' 'LGPL')
depends=('libxt' 'libpng' 'alsa-lib' 'gtk2' 'gtk3' 'startup-notification' 'nss' 'icu' 'libvpx' 'libevent' 'sqlite' 'libatk' 'dbus-glib' 'ttf-font')
makedepends=('python2' 'which' 'unzip' 'zip' 'autoconf2.13' 'yasm' 'diffutils' 'mesa' 'inetutils' 'xorg-server' 'alsa-lib-dev' 'atk-dev' 'wireless_tools')
optdepends=('networkmanager: Location detection via available WiFi networks'
			'libnotify: Notification integration'
			'pulseaudio: Audio support'
			'speech-dispatcher: Text-to-Speech'
			'wireless_tools: Geolocation web services')
options=('!emptydirs' '!makeflags' '!strip')
source=("https://ftp.mozilla.org/pub/mozilla.org/${pkgname}/releases/${pkgver}/source/${pkgname}-${pkgver}.source.tar.xz"
		'mozconfig'
		'firefox.desktop'
		'install-dir.patch'
		'mozilla-key'
		'google-key')
md5sums=('16be3d9774f1e3bf74e2c4e198652d19'
         '7420a35515815034a1d3f41b5f297c18'
         '6aa9f410d182185b3cb2900d0201029d'
         'dbf14588e85812ee769bd735823a0146'
         '9c2c2c1afaee368765e21b9cf7be58d4'
         'ca364d4c9b68942b094ad109732e08a3')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}
	cp ${srcdir}/mozconfig .mozconfig
	
	patch -Np1 -i ${srcdir}/install-dir.patch

	# this is needed if using system libevent
	sed -e s/_EVENT_SIZEOF/EVENT__SIZEOF/ -i ipc/chromium/src/base/message_pump_libevent.cc

	mkdir "${srcdir}/path"
	ln -s /usr/bin/python2 "${srcdir}/path/python"
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# _FORTIFY_SOURCE causes configure failures
	CPPFLAGS+=" -O2"

	# Hardening
	LDFLAGS+=" -Wl,-z,now"

	# GCC 6
	#CXXFLAGS+=" -fno-delete-null-pointer-checks -fno-schedule-insns2"

	export PATH="${srcdir}/path:${PATH}"

	make -f client.mk
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make -f client.mk DESTDIR="${pkgdir}" INSTALL_SDK= install

	_vendorjs="${pkgdir}/usr/lib/${pkgname}/browser/defaults/preferences/vendor.js"
	install -Dm644 /dev/stdin "${_vendorjs}" <<END
// Use LANG environment variable to choose locale
pref("intl.locale.matchOS", true);

// Disable default browser checking.
pref("browser.shell.checkDefaultBrowser", false);

// Don't disable our bundled extensions in the application directory
pref("extensions.autoDisableScopes", 11);
pref("extensions.shownSelectionUI", true);

// Opt all of us into e10s, instead of just 50%
pref("browser.tabs.remote.autostart", true);
END

	_distini="${pkgdir}/usr/lib/${pkgname}/distribution/distribution.ini"
	install -Dm644 /dev/stdin "${_distini}" <<END
[Global]
id=cromnix
version=1.0
about=Mozilla Firefox for Cromnix

[Preferences]
app.distributor=cromnix
app.distributor.channel=${pkgname}
app.partner.cromnix=cromnix
END

	mkdir -pv ${pkgdir}/usr/share/applications
	mkdir -pv ${pkgdir}/usr/share/pixmaps

	install -Dm644 ${srcdir}/firefox.desktop ${pkgdir}/usr/share/applications/firefox.desktop

	ln -sfv /usr/lib/${pkgname}/browser/icons/mozicon128.png ${pkgdir}/usr/share/pixmaps/firefox.png
	
	install -Dm755 /dev/stdin "${pkgdir}/usr/bin/${pkgname}" <<END
#!/bin/sh
exec /usr/lib/${pkgname}/firefox "\$@"
END

	ln -srf "${pkgdir}/usr/bin/${pkgname}" "${pkgdir}/usr/lib/${pkgname}/firefox-bin"
}
