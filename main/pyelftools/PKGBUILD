# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=pyelftools
pkgver=0.24
_pyver=3.6
pkgrel=1
pkgdesc='Python library for analyzing ELF files and DWARF debugging information'
arch=('any')
url='https://github.com/eliben/pyelftools'
license=('custom:Public Domain')
depends=('python')
options=('!strip')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/eliben/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('c9797c996550288daca7ce7d44deabc2')

build() {
	cd ${pkgname}-${pkgver}
	python3 setup.py build
}

check() {
	cd ${pkgname}-${pkgver}
	python3 test/run_all_unittests.py
	python3 test/run_examples_test.py
	python3 test/run_readelf_tests.py
}

package() {
	cd ${pkgname}-${pkgver}
	python3 setup.py install -O1 --root="${pkgdir}" --skip-build
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
	cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}
