#Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=gpm
pkgname=('gpm' 'gpm-openrc')
pkgver=1.20.7
pkgrel=4
pkgdesc="The GPM (General Purpose Mouse daemon) package contains a mouse server for the console and xterm."
arch=('x86_64')
url="http://www.nico.schottelius.org/software/gpm/"
license=('GPL')
makedepends=('openrc-services')
source=("http://www.nico.schottelius.org/software/gpm/archives/$pkgbase-$pkgver.tar.bz2"
		"https://gitweb.gentoo.org/repo/gentoo.git/plain/sys-libs/gpm/files/gpm.rc6-2"
		"https://gitweb.gentoo.org/repo/gentoo.git/plain/sys-libs/gpm/files/gpm.conf.d")
md5sums=('bf84143905a6a903dbd4d4b911a2a2b8'
         '9404f8eb54a47f4129e280a0c199093a'
         '5968cb47e493e348cf289b0dd1990f4a')

build() {
	cd "$srcdir/$pkgbase-$pkgver"
	./autogen.sh
	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc
	make
}

package_gpm() {
	depends=('gpm-init' 'glibc')
	cd "$srcdir/$pkgbase-$pkgver"
	make DESTDIR="$pkgdir" install

	install-info --dir-file=${pkgdir}/usr/share/info/dir ${pkgdir}/usr/share/info/gpm.info

	ln -sfv libgpm.so.2.1.0 ${pkgdir}/usr/lib/libgpm.so
	install -v -m644 conf/gpm-root.conf ${pkgdir}/etc

	install -v -m755 -d ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/support
	install -v -m644 doc/support/* ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/support
	install -v -m644 doc/{FAQ,HACK_GPM,README*} ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
}

package_gpm-openrc() {
	pkgdesc="OpenRC init scripts for gpm"
	arch=('any')
	depends=('openrc' 'openrc-services')
	provides=('gpm-init')
	install -v -m755 -d ${pkgdir}/etc/openrc/init.d
	install -v -m755 -d ${pkgdir}/etc/openrc/conf.d
	install -v -m755 ${srcdir}/gpm.rc6-2 ${pkgdir}/etc/openrc/init.d/gpm
	install -v -m644 ${srcdir}/gpm.conf.d ${pkgdir}/etc/openrc/conf.d/gpm

	install -Dm644 /usr/share/openrc-services/hooks/svc-openrc-install.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-install.hook
	install -Dm644 /usr/share/openrc-services/hooks/svc-openrc-upgrade.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-upgrade.hook
	install -Dm644 /usr/share/openrc-services/hooks/svc-openrc-remove.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-remove.hook
	install -Dm755 /usr/share/openrc-services/scripts/svc-openrc.script "${pkgdir}"/usr/share/libalpm/scripts/${pkgbase}-openrc
	
	sed -e "s/@svc@/${pkgbase}/" -i "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-{install,upgrade,remove}.hook
	sed -e "s/@svc_enable@/'${pkgbase}'/" \
		-e "s/@svc_run@/'${pkgbase}'/" \
		-e "s/@svc_remove@/'${pkgbase}'/" \
		-e "s/@svlevel@/default/" \
		-i "${pkgdir}"/usr/share/libalpm/scripts/${pkgbase}-openrc
}
