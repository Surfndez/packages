# Maintainer: Chris Cromer <chris@cromer.cl>

pkgbase=eudev
pkgname=('eudev' 'eudev-openrc')
pkgver=3.2.2
_openrc_pkgver=32
pkgrel=2
pkgdesc="The Eudev package contains programs for dynamic creation of device nodes."
arch=('x86_64')
url="https://wiki.gentoo.org/wiki/Project:Eudev"
license=('GPL')
makedepends=('gperf')
source=("http://dev.gentoo.org/~blueness/eudev/$pkgbase-$pkgver.tar.gz"
		"https://dev.gentoo.org/~williamh/dist/udev-init-scripts-${_openrc_pkgver}.tar.gz"
		'udevadm-hwdb.hook'
		'eudev-openrc-install.hook'
		'eudev-openrc-upgrade.hook'
		'eudev-openrc.script')
md5sums=('41e19b70462692fefd072a3f38818b6e'
         'a8098437126a0ecdc23f83e923de3644'
         'cc11561625e862c4f24f964a1015f530'
         '48913130da42fc05fa2029ef966caccb'
         '43027a271e5da0dfa9cd944e04f11d54'
         '18627e4134720794d34ad4323e58d9e0')

prepare() {
	cd "$srcdir/$pkgbase-$pkgver"

	sed -r -i 's|/usr(/bin/test)|\1|' test/udev-test.pl
}

build() {
	cd "$srcdir/$pkgbase-$pkgver"
	./configure --prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libdir=/usr/lib \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--with-rootprefix= \
		--with-rootlibdir=/usr/lib \
		--enable-manpages \
		--disable-static \
		--config-cache
	make
}

check() {
	cd "$srcdir/$pkgbase-$pkgver"
	make check
}

package_eudev() {
	groups=('cromnix-base')
	depends=('eudev-init' 'kmod' 'util-linux' 'glibc' 'coreutils')
	cd "$srcdir/$pkgbase-$pkgver"

	make DESTDIR="$pkgdir" install

	install -Dm644 ${srcdir}/udevadm-hwdb.hook ${pkgdir}/usr/share/libalpm/hooks/udevadm-hwdb.hook
}

package_eudev-openrc() {
	pkgdesc="OpenRC init scripts for eudev"
	arch=('any')
	depends=('openrc')
	provides=('eudev-init')
	install=eudev-openrc.install
	install -v -m755 -d ${pkgdir}/etc/openrc/init.d
	install -v -m755 -d ${pkgdir}/etc/openrc/conf.d
	install -v -m755 ${srcdir}/udev-init-scripts-${_openrc_pkgver}/init.d/* ${pkgdir}/etc/openrc/init.d
	install -v -m755 ${srcdir}/udev-init-scripts-${_openrc_pkgver}/conf.d/* ${pkgdir}/etc/openrc/conf.d
	
	install -Dm644 ${srcdir}/eudev-openrc-install.hook ${pkgdir}/usr/share/libalpm/hooks/eudev-openrc-install.hook
	install -Dm644 ${srcdir}/eudev-openrc-upgrade.hook ${pkgdir}/usr/share/libalpm/hooks/eudev-openrc-upgrade.hook
	install -Dm755 ${srcdir}/eudev-openrc.script ${pkgdir}/usr/share/libalpm/scripts/eudev-openrc
}
