# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=expect
pkgver=5.45
pkgrel=1
pkgdesc="It contains tools for automating interactive applications such as telnet, ftp, passwd, fsck, rlogin, tip, etc. Expect is also useful for testing these same applications as well as easing all sorts of tasks that are prohibitively difficult with anything else."
arch=('x86_64')
url="http://www.nist.gov/el/msid/expect.cfm"
license=('custom')
depends=('tcl')
source=("http://prdownloads.sourceforge.net/expect/$pkgname$pkgver.tar.gz")
md5sums=('44e1a4f4c877e9ddc5a542dfa7ecc92b')

build() {
    cd "$srcdir/$pkgname$pkgver"
    ./configure --prefix=/usr \
        --with-tcl=/usr/lib \
        --enable-shared \
        --mandir=/usr/share/man \
        --with-tclinclude=/usr/include
    make
}

check() {
    cd "$srcdir/$pkgname$pkgver"
    make test
}

package() {
    cd "$srcdir/$pkgname$pkgver"
    make DESTDIR="$pkgdir" install

    ln -svf ${pkgname}${pkgver}/libexpect${pkgver}.so ${pkgdir}/usr/lib/libexpect${pkgver}.so

    install -D -m644 README ${pkgdir}/usr/share/licenses/${pkgname}/README
}
