# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=harfbuzz
pkgver=1.4.8
pkgrel=2
pkgdesc='The HarfBuzz package contains an OpenType text shaping engine'
arch=('x86_64')
url='http://www.freedesktop.org/wiki/Software/HarfBuzz'
license=('MIT')
depends=('glib' 'freetype')
optdepends=('cairo: hb-view program')
makedepends=('glib' 'freetype' 'icu' 'python2' 'gobject-introspection' 'gtk-doc' 'cairo')
source=("http://www.freedesktop.org/software/harfbuzz/release/${pkgname}-${pkgver}.tar.bz2")
md5sums=('d1aa446e1e65717311c15d9ac0cf31ee')

# todo: rebuild split package

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr \
		--with-cairo \
		--with-freetype \
		--with-glib \
		--with-gobject \
		--with-icu \
		--enable-gtk-doc
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}
