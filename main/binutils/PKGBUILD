# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=binutils
pkgver=2.28
pkgrel=1
pkgdesc='Linker, assembler, and other tools'
arch=('x86_64')
url='http://www.gnu.org/software/binutils/'
license=('GPL')
groups=('cromnix-dev')
depends=('glibc>=2.25' 'zlib')
checkdepends=('dejagnu' 'bc')
options=('staticlibs' '!distcc' '!ccache')
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('9e8340c96626b469a603c15c9d843727')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}
    mkdir -pv ${srcdir}/${pkgname}-${pkgver}/build

    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}/build
    ../configure --prefix=/usr \
		--enable-gold \
		--enable-ld=default \
		--enable-plugins \
        --enable-shared \
        --libdir=/usr/lib \
        --disable-werror
    make tooldir=/usr
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver}/build
    make -k check
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}/build
    make DESTDIR=${pkgdir} tooldir=/usr install
}
