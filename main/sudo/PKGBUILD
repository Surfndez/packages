#Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=sudo
pkgver=1.8.20.p2
_pkgver=1.8.20p2
pkgrel=2
pkgdesc="The Sudo package allows a system administrator to give certain users (or groups of users) the ability to run some (or all) commands as root or another user while logging the commands and arguments."
arch=('x86_64')
url="http://www.sudo.ws"
license=('custom')
groups=('cromnix-dev')
depends=('glibc')
backup=('etc/sudoers')
install=sudo.install
source=("http://www.sudo.ws/dist/$pkgname-$_pkgver.tar.gz")
md5sums=('03da8e711caca6fd93e57751bfb74adc')

build() {
    cd "$srcdir/$pkgname-$_pkgver"
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --sbindir=/usr/bin \
        --with-secure-path \
        --with-env-editor \
        --docdir=/usr/share/doc/$pkgname-$pkgver \
        --with-passprompt="[sudo] password for %p: " \
        --libdir=/usr/lib
    make
}

check() {
    cd "$srcdir/$pkgname-$_pkgver"
    env LC_ALL=C make check 2>&1 | tee ../make-check.log
}

package() {
    cd "$srcdir/$pkgname-$_pkgver"
    make DESTDIR="$pkgdir" install
    ln -sfv libsudo_util.so.0.0.0 ${pkgdir}/usr/lib/sudo/libsudo_util.so.0

    rm -rf ${pkgdir}/var/run
}
