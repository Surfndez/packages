# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=pamac
pkgver=6.0.1
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3' 'gtk3'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'gnutls'
         'appstream-glib' 'cromnix-appstream-data')
makedepends=('gettext' 'itstool' 'vala>=0.36' 'atk-dev')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
			'polkit-mate: needed for authentification in Mate'
			'lxsession: needed for authentification in Xfce, LXDE etc.'
			'pamac-tray-appindicator: tray icon for KDE')
backup=('etc/pamac.conf')
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
		'disable-aur.patch')
md5sums=('98869851a3b7010ab45798a057953541'
         '98f3907136a72c02712399c9c88d544b')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	# patches here
	#patch -Np1 -i "${srcdir}"/disable-aur.patch
	sed -e "s|archlinux-arch-|cromnix-|" -i src/user_daemon.vala
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	make all
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install

	rm -rf "${pkgdir}"/usr/lib/systemd
}
