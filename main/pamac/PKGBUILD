# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=pamac
pkgver=6.0.3
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm"
arch=('x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3' 'gtk3'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'gnutls'
         'appstream-glib' 'cromnix-appstream-data')
makedepends=('gettext' 'itstool' 'vala>=0.36' 'atk-dev')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
			'mate-polkit: needed for authentification in Mate'
			'lxsession: needed for authentification in Xfce, LXDE etc.'
			'pamac-tray-appindicator: tray icon for KDE')
backup=('etc/pamac.conf')
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
		'disable-aur.patch')
md5sums=('c11dfa80ecd48c62716dc16f0b826bf2'
         'af51d90497a411473b099256f8ba6309')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	# patches here
	patch -Np1 -i "${srcdir}"/disable-aur.patch
	sed -e "s|archlinux-arch-|cromnix-|" -i src/user_daemon.vala
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	make all
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install
	rm -rf "${pkgdir}"/etc/systemd
	rm -rf "${pkgdir}"/usr/lib/systemd
}
