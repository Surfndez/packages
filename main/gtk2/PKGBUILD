#Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=gtk2
pkgver=2.24.31
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='The GTK+ 2 package contains libraries used for creating graphical user interfaces for applications'
arch=('x86_64')
url='http://www.gtk.org/'
license=('LGPL')
depends=('libatk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'desktop-file-utils' 'gdk-pixbuf')
makedepends=('atk-dev' 'python2')
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${_pkgver}/gtk+-${pkgver}.tar.xz"
		'gtk-query-immodules-2.0.hook'
		'gtkrc')
install='gtk2.install'
md5sums=('526a1008586094a2cbb4592fd3f9ee10'
         '01735166750a40a539d4bad5dbd5f664'
         'dd4fe16d8099fb2d535eaab80035b78e')

# todo: many recommeneded and optional dependencies need to be added once they are created

prepare() {
	cd "${srcdir}/gtk+-${pkgver}"
	sed -e 's#l \(gtk-.*\).sgml#& -o \1#' -i docs/{faq,tutorial}/Makefile.in
	sed -i '1s/python$/&2/' gtk/gtk-builder-convert
}

build() {
	cd "${srcdir}/gtk+-${pkgver}"
	./configure --prefix=/usr \
		--sysconfdir=/etc
	make
}

package() {
	cd "${srcdir}/gtk+-${pkgver}"
	make DESTDIR="${pkgdir}" install

    install -Dm644 "${srcdir}/gtkrc" "$pkgdir/usr/share/gtk-2.0/gtkrc"
    install -Dm644 "${srcdir}/gtk-query-immodules-2.0.hook" "${pkgdir}/usr/share/libalpm/hooks/gtk-query-immodules-2.0.hook"

	rm "${pkgdir}/usr/bin/gtk-update-icon-cache"
	rm -rf "${pkgdir}/usr/share/gtk-doc"
}
