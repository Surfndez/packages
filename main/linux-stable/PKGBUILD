# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=linux-stable
pkgname=('linux-stable' 'linux-stable-headers' 'linux-stable-doc')
pkgver=4.13.3
pkgrel=1
pkgdesc="This package contain the stable linux kernel, support for this kernel is not guaranteed and may end up EOL(End of Life)"
arch=('x86_64')
url="http://www.kernel.org/"
license=('GPL2')
makedepends=('xmlto' 'kmod' 'inetutils' 'bc')
options=('!strip')
source=("https://www.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz"
        "config"
        "linux-lib-path.patch")
md5sums=('ab0ffc872db83b7fd2f5ffa089c81ca9'
         'f33314914ad78ec4da524ff03b9f1c8a'
         '9d34da065c7c15ab5aa73fa4b44f4c21')

prepare() {
    cd "$srcdir/linux-$pkgver"
    make mrproper

    cp -v ${srcdir}/config .config
    #make olddefconfig
	#make menuconfig
    patch -Np1 -i ${srcdir}/linux-lib-path.patch

    sed -i '2iexit 0' scripts/depmod.sh

	sed -ri "s|^(EXTRAVERSION =).*|\1 -${pkgrel}|" Makefile
}

build() {
    cd "$srcdir/linux-$pkgver"
    make LOCALVERSION= bzImage modules
}

package_linux-stable() {
	optdepends=('linux-stable-doc: linux documentation')

    cd "$srcdir/linux-$pkgver"

	_kernelversion=$(make LOCALVERSION= kernelrelease)
	sed -e "s|@VERSION@|${_kernelversion}|g" \
		"${startdir}/linux.in" > "${startdir}/linux.install"
		true && install=linux.install

	mkdir -pv ${pkgdir}/usr/lib
    ln -sv ${pkgdir}/usr/lib ${pkgdir}/lib

    make LOCALVERSION= INSTALL_MOD_PATH="${pkgdir}" modules_install

    mkdir -pv ${pkgdir}/boot
    cp -v arch/x86/boot/bzImage ${pkgdir}/boot/vmlinuz-${pkgver}-${pkgrel}-cromnix-stable
    cp -v System.map ${pkgdir}/boot/System.map-${pkgver}-${pkgrel}-cromnix-stable
    cp -v .config ${pkgdir}/boot/config-${pkgver}-${pkgrel}-cromnix-stable

    install -vd -m755 ${pkgdir}/etc/modprobe.d

	rm -rf "${pkgdir}"/usr/lib/firmware
	rm -rf "${pkgdir}"/usr/lib/modules/${_kernelversion}/{build,source}

    cd ${pkgdir}/boot

    depmod -b "${pkgdir}" -F "${pkgdir}/boot/System.map-${pkgver}-${pkgrel}-cromnix-stable" "${_kernelversion}"

    rm -v ${pkgdir}/lib
}

package_linux-stable-headers() {
	pkgdesc="Header files and scripts for building modules for the stable line kernel"

	cd "$srcdir/linux-$pkgver"
	local _builddir="${pkgdir}/usr/lib/modules/${_kernelversion}/build"

	install -Dt "${_builddir}" -m644 Makefile .config Module.symvers
	install -Dt "${_builddir}/kernel" -m644 kernel/Makefile

	mkdir "${_builddir}/.tmp_versions"

	cp -t "${_builddir}" -a include scripts

	install -Dt "${_builddir}/arch/x86" -m644 arch/x86/Makefile
	install -Dt "${_builddir}/arch/x86/kernel" -m644 arch/x86/kernel/asm-offsets.s

	cp -t "${_builddir}/arch/x86" -a arch/x86/include

	install -Dt "${_builddir}/drivers/md" -m644 drivers/md/*.h
	install -Dt "${_builddir}/net/mac80211" -m644 net/mac80211/*.h

	# http://bugs.archlinux.org/task/9912
	install -Dt "${_builddir}/drivers/media/dvb-core" -m644 drivers/media/dvb-core/*.h

	# http://bugs.archlinux.org/task/13146
	install -Dt "${_builddir}/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/lgdt330x.h
	install -Dt "${_builddir}/drivers/media/i2c" -m644 drivers/media/i2c/msp3400-driver.h

	# http://bugs.archlinux.org/task/20402
	install -Dt "${_builddir}/drivers/media/usb/dvb-usb" -m644 drivers/media/usb/dvb-usb/*.h
	install -Dt "${_builddir}/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/*.h
	install -Dt "${_builddir}/drivers/media/tuners" -m644 drivers/media/tuners/*.h

	# add xfs and shmem for aufs building
	mkdir -p "${_builddir}"/{fs/xfs,mm}

	# copy in Kconfig files
	find . -name Kconfig\* -exec install -Dm644 {} "${_builddir}/{}" \;

	# add objtool for external module building and enabled VALIDATION_STACK option
	if [[ -e tools/objtool/objtool ]]; then
		install -Dt "${_builddir}/tools/objtool" tools/objtool/objtool
	fi

	# remove unneeded architectures
	local _arch
	for _arch in "${_builddir}"/arch/*/; do
		if [[ ${_arch} != */x86/ ]]; then
			rm -r "${_arch}"
		fi
	done

	# remove files already in linux-docs package
	rm -r "${_builddir}/Documentation"

	# Fix permissions
	chmod -R u=rwX,go=rX "${_builddir}"

	# strip scripts directory
	local _binary _strip
	while read -rd '' _binary; do
		case "$(file -bi "${_binary}")" in
			*application/x-sharedlib*)  _strip="${STRIP_SHARED}"   ;; # Libraries (.so)
			*application/x-archive*)    _strip="${STRIP_STATIC}"   ;; # Libraries (.a)
			*application/x-executable*) _strip="${STRIP_BINARIES}" ;; # Binaries
			*) continue ;;
		esac
		/usr/bin/strip ${_strip} "${_binary}"
	done < <(find "${_builddir}/scripts" -type f -perm -u+w -print0 2>/dev/null)
}

package_linux-stable-doc() {
	arch=('any')

    cd "$srcdir/linux-$pkgver"

	mkdir -pv ${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}
    cp -r Documentation/* ${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}
}
