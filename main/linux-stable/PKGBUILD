# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=linux-stable
pkgname=('linux-stable' 'linux-stable-doc')
pkgver=4.12.10
pkgrel=1
pkgdesc="This package contain the stable linux kernel, support for this kernel is not guaranteed and may end up EOL(End of Life)"
arch=('x86_64')
url="http://www.kernel.org/"
license=('GPL2')
makedepends=('docbook-xsl' 'kmod' 'inetutils' 'bc')
options=('!strip')
source=("https://www.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz"
        "config"
        "usb.conf"
        "linux-lib-path.patch")
md5sums=('8cfe8bd3bd00c0bb10171441e1a9c99e'
         '999cbf3ec7a100acbee4169ee0efeccb'
         '30994cd9cba3aa218b3afa33d34eb649'
         '9d34da065c7c15ab5aa73fa4b44f4c21')

prepare() {
    cd "$srcdir/linux-$pkgver"
    make mrproper

    cp -v ${srcdir}/config .config
    #make olddefconfig
	#make menuconfig
    patch -Np1 -i ${srcdir}/linux-lib-path.patch

    sed -i '2iexit 0' scripts/depmod.sh

	sed -ri "s|^(EXTRAVERSION =).*|\1 -${pkgrel}|" Makefile
}

build() {
    cd "$srcdir/linux-$pkgver"
    make LOCALVERSION= bzImage modules
}

package_linux-stable() {
	groups=('kernel')
	optdepends=('linux-stable-doc: linux documentation')

    cd "$srcdir/linux-$pkgver"

	_kernelversion=$(make LOCALVERSION= kernelrelease)
	sed -e "s|@VERSION@|${_kernelversion}|g" \
		"${startdir}/linux.in" > "${startdir}/linux.install"
		true && install=linux.install

	mkdir -pv ${pkgdir}/usr/lib
    ln -sv ${pkgdir}/usr/lib ${pkgdir}/lib

    make LOCALVERSION= INSTALL_MOD_PATH="${pkgdir}" modules_install

    mkdir -pv ${pkgdir}/boot
    cp -v arch/x86/boot/bzImage ${pkgdir}/boot/vmlinuz-${pkgver}-${pkgrel}-cromnix-stable
    cp -v System.map ${pkgdir}/boot/System.map-${pkgver}-${pkgrel}-cromnix-stable
    cp -v .config ${pkgdir}/boot/config-${pkgver}-${pkgrel}-cromnix-stable

    install -vd -m755 ${pkgdir}/etc/modprobe.d

	rm -rf "${pkgdir}/usr/lib/firmware"

    cd ${pkgdir}/boot

    depmod -b "${pkgdir}" -F "${pkgdir}/boot/System.map-${pkgver}-${pkgrel}-cromnix-stable" "${_kernelversion}"

    rm -v ${pkgdir}/lib
}

package_linux-stable-doc() {
	groups=('kernel-doc')
	arch=('any')

    cd "$srcdir/linux-$pkgver"

	mkdir -pv ${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}
    cp -r Documentation/* ${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}
}
