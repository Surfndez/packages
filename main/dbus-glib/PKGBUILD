# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=dbus-glib
pkgver=0.108
pkgrel=1
pkgdesc='The D-Bus GLib package contains GLib interfaces to the D-Bus API.'
arch=('x86_64')
url='http://www.freedesktop.org/wiki/Software/DBusBindings'
license=('GPL')
depends=('dbus' 'glib')
options=('!emptydirs')
source=("http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('a66a613705870752ca9786e0359aea97')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--disable-static
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	mkdir -pv "${pkgdir}"/usr/share/bash-completion/completions
	mv "${pkgdir}"/etc/bash_completion.d/dbus-bash-completion.sh "${pkgdir}"/usr/share/bash-completion/completions/dbus
	rm -rf "${pkgdir}"/etc/bash_completion.d
}
