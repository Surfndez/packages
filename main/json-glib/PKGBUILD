# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=json-glib
pkgver=1.4.2
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='The JSON GLib package is a library providing serialization and deserialization support for the JavaScript Object Notation (JSON) format described by RFC 4627'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/JsonGlib'
license=('GPL')
depends=('glib')
makedepends=('gobject-introspection' 'gtk-doc' 'meson')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('35107e23a7bbbc70f31c34f7b9adf1c3')

prepare() {
	mkdir -pv "${srcdir}/_build"
	cd "${srcdir}/${pkgname}-${pkgver}"
}

build() {
	cd "${srcdir}/_build"
	meson setup --prefix=/usr --buildtype=release "${srcdir}/${pkgname}-${pkgver}" \
		--libexecdir=/usr/lib \
		-Denable-gtk-doc=true \
		-Denable-man=true
	ninja
}

check() {
	cd "${srcdir}/_build"
	meson test
}

package() {
	cd "${srcdir}/_build"
	DESTDIR="${pkgdir}" ninja install
	sed -e "s|${srcdir}/_build||" -i "${pkgdir}"/usr/include/json-glib-1.0/json-glib/json-enum-types.h
}
