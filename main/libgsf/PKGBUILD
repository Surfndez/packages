# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libgsf
pkgver=1.14.41
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='The libgsf package contains a library used for providing an extensible input/output abstraction layer for structured file formats'
arch=('x86_64')
url='https://git.gnome.org/browse/libgsf'
license=('GPL' 'LGPL')
depends=('libxml2' 'gdk-pixbuf' 'bzip2')
makedepends=('intltool')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('5b039d1b7196e643315abda1c1c349c4')

# todo: rebuild with full depends

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--disable-static
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
