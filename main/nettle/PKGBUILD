# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=nettle
pkgver=3.3
pkgrel=1
pkgdesc="The Nettle package contains the low-level cryptographic library that is designed to fit easily in many contexts."
arch=('x86_64')
url="http://www.lysator.liu.se/~nisse/nettle/"
license=('GPL')
depends=('gmp')
source=("https://ftp.gnu.org/gnu/nettle/$pkgname-$pkgver.tar.gz")
md5sums=('10f969f78a463704ae73529978148dbe')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr \
        --disable-static \
        --libdir=/usr/lib
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    chmod -v 755 ${pkgdir}/usr/lib/lib{hogweed,nettle}.so
    install -v -m755 -d ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
    install -v -m644 nettle.html ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
}
