# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=tcl
pkgver=8.6.6
pkgrel=1
pkgdesc="The Tcl package contains the Tool Command Language, a robust general-purpose scripting language."
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=('zlib')
options=('staticlibs')
source=("http://downloads.sourceforge.net/tcl/$pkgname$pkgver-src.tar.gz"
        "http://downloads.sourceforge.net/tcl/$pkgname$pkgver-html.tar.gz")
md5sums=('5193aea8107839a79df8ac709552ecb7'
         '29b28a8c98a97f34bcb92af3222d253f')

build() {
    cd "$srcdir/$pkgname$pkgver/unix"

    ./configure --prefix=/usr \
        --mandir=/usr/share/man \
        --enable-64bit
    make

    sed -e "s#$srcdir/$pkgname$pkgver/unix#/usr/lib#" \
        -e "s#$srcdir/$pkgname$pkgver#/usr/include#" \
        -i tclConfig.sh

    sed -e "s#$srcdir/$pkgname$pkgver/unix/pkgs/tdbc1.0.4#/usr/lib/tdbc1.0.4#" \
        -e "s#$srcdir/$pkgname$pkgver/pkgs/tdbc1.0.4/generic#/usr/include#" \
        -e "s#$srcdir/$pkgname$pkgver/pkgs/tdbc1.0.4/library#/usr/lib/tcl8.6#" \
        -e "s#$srcdir/$pkgname$pkgver/pkgs/tdbc1.0.4#/usr/include#" \
        -i pkgs/tdbc1.0.4/tdbcConfig.sh

    sed -e "s#$srcdir/$pkgname$pkgver/unix/pkgs/itcl4.0.5#/usr/lib/itcl4.0.5#" \
        -e "s#$srcdir/$pkgname$pkgver/pkgs/itcl4.0.5/generic#/usr/include#" \
        -e "s#$srcdir/$pkgname$pkgver/pkgs/itcl4.0.5#/usr/include#" \
        -i pkgs/itcl4.0.5/itclConfig.sh
}

check() {
    cd "$srcdir/$pkgname$pkgver/unix"
    make test
}

package() {
    cd "$srcdir/$pkgname$pkgver/unix"

    make DESTDIR="$pkgdir" install

    make DESTDIR="$pkgdir" install-private-headers
    ln -v -sf tclsh8.6 ${pkgdir}/usr/bin/tclsh
    chmod -v 755 ${pkgdir}/usr/lib/libtcl8.6.so

    mkdir -v -p ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
    cp -v -r  ../html/* ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
}
