# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=meson
pkgver=0.42.1
pkgrel=2
pkgdesc='High productivity build system'
arch=('any')
url='http://mesonbuild.com/'
license=('Apache')
depends=('python' 'ninja')
makedepends=('python-setuptools')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt4' 'qt5-base' 'git' 'gnustep-base'
              'cython' 'gtkmm3' 'gtest' 'gmock' 'protobuf' 'wxgtk' 'python-gobject' 'gobject-introspection'
              'itstool' 'gtk3' 'valgrind' 'java-environment>=8' 'gtk-doc' 'llvm' 'clang' 'sdl2'
              'doxygen' 'vulkan-validation-layers' 'openmpi' 'openssh' 'mercurial' 'ldc')
source=("https://github.com/mesonbuild/meson/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('a8ff06ab5e138dec64cbfa249ff4f8bb')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python setup.py build
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	unset CLASSPATH  # GNUstep breaks java tests
	LDFLAGS="${LDFLAGS/,--as-needed/}" # --as-needed breaks openmpi linking
	MESON_PRINT_TEST_OUTPUT=1 LC_CTYPE=en_US.UTF-8 ./run_tests.py
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
