# Maintainer: Chris Cromer <chris@cromer.cl>

pkgbase=('nano')
pkgname=('nano' 'nano-cromnix' 'nano-vanilla')
pkgver=2.8.4
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="http://www.nano-editor.org"
source=(http://www.nano-editor.org/dist/v${_pkgver}/${pkgbase}-${pkgver}.tar.gz)
md5sums=('e2a6bfc054d1f535b723f93eb73e5f77')

build() {
	cd ${pkgbase}-${pkgver}
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-utf8 \
		--enable-color \
		--enable-nanorc \
		--enable-multibuffer \
		--disable-wrapping-as-root \
		--docdir=/usr/share/doc/nano-${pkgver}
	make
}

package_nano() {
	# meta package
	depends=('nano-bin')
	arch=('any')
}

package_nano-vanilla() {
	depends=('ncurses' 'file' 'sh')
	backup=('etc/nanorc')
	provides=('nano-bin')
	conflicts=('nano-cromnix')
	cd ${pkgbase}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -v -m644 doc/nano.html ${pkgdir}/usr/share/doc/nano-${pkgver}
	install -DTm644 ${srcdir}/${pkgbase}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc
}

package_nano-cromnix() {
	depends=('ncurses' 'file' 'sh')
	backup=('etc/nanorc')
	provides=('nano-bin')
	conflicts=('nano-vanilla')
	cd ${pkgbase}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -v -m644 doc/nano.html ${pkgdir}/usr/share/doc/nano-${pkgver}
	install -DTm644 ${srcdir}/${pkgbase}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc

	sed -e 's|# set autoindent|set autoindent|' \
		-e 's|# set constantshow|set constantshow|' \
		-e 's|# set tabsize 8|set tabsize 4|' \
		-e 's|# set multibuffer|set multibuffer|' \
		-e 's|# set nowrap|set nowrap|' \
		-e 's|# include "/usr/share/nano/\*.nanorc"|include "/usr/share/nano/*.nanorc"|' \
		-i ${pkgdir}/etc/nanorc
}
