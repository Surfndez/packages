# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=openssh
pkgver=7.6.p1
_pkgver=7.6p1
pkgrel=1
pkgdesc='The OpenSSH package contains ssh clients and the sshd daemon'
arch=('x86_64')
url='https://www.openssh.com/portable.html'
license=('custom:BSD')
depends=('openssl')
optdepends=('xorg-xauth: X11 forwarding'
			'x11-ssh-askpass: input passphrase in X')
source=("http://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/${pkgname}-${_pkgver}.tar.gz"
		"${pkgname}-${_pkgver}-openssl-1.1.0-1.patch")
md5sums=('06a88699018e5fef13d4655abfed1f63'
         '0ac61cf4dad69ff15a9a80423579fa03')

prepare() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	patch -Np1 -i "${srcdir}"/${pkgname}-${_pkgver}-openssl-1.1.0-1.patch
}

build() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/ssh \
		--sysconfdir=/etc/ssh \
		--with-md5-passwords \
		--with-privsep-path=/var/lib/sshd
	make
}

check() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	if [[ -e /usr/bin/scp ]]; then
		make tests
	fi
}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	make DESTDIR="${pkgdir}" install
	install -v -m755 contrib/ssh-copy-id "${pkgdir}"/usr/bin
	install -v -m644 contrib/ssh-copy-id.1 "${pkgdir}"/usr/share/man/man1
	install -v -m755 -d "${pkgdir}"/usr/share/doc/${pkgname}-${_pkgver}
	install -v -m644 INSTALL LICENCE OVERVIEW README* "${pkgdir}"/usr/share/doc/${pkgname}-${_pkgver}
}
