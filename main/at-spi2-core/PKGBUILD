# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=at-spi2-core
pkgver=2.24.1
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc='The At-Spi2 Core package is a part of the GNOME Accessibility Project'
arch=('x86_64')
url='http://www.gnome.org'
license=('GPL2')
depends=('dbus' 'glib' 'libxtst')
makedepends=('intltool')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${_pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('61d0a471e693292934a73f288ebff35c')

# todo: rebuild with gtk-doc
# todo: there is a systemd service included with no openrc alternative

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--libdir=/usr/lib \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/at-spi2-core
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# remove the systemd unit
	rm -rf ${pkgdir}/usr/lib/systemd
}
