# Maintainer: Chris Cromer <chris@cromer.cl>

pkgbase=freefall
pkgname=('freefall' 'freefall-cromnix' 'freefall-vanilla' 'freefall-openrc')
pkgver=4.11.3
pkgrel=1
pkgdesc="Disk protection for drops on HP/Dell laptops"
arch=('x86_64')
url="https://www.kernel.org/"
license=('GPL2')
source=("https://www.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz"
		"https://gitweb.gentoo.org/repo/gentoo.git/plain/sys-apps/linux-misc-apps/files/freefall.initd"
		"https://gitweb.gentoo.org/repo/gentoo.git/plain/sys-apps/linux-misc-apps/files/freefall.confd"
		'freefall-service.hook'
		'freefall-service.script')
md5sums=('d8f9218277a3f0d2e1703676002be428'
         '76762cb551df73ceb6a8aab8e942f9c9'
         '3c2d2bdd54736b440ed47d90583b172e'
         '563728b320ce5c5ee8081146200b5075'
         '70fbfb579ab432e2c11cbd06a289d292')

build() {
	cd "$srcdir/linux-$pkgver/tools/laptop/freefall"
	make
}

package_freefall() {
	# meta package
	depends=('freefall-bin')
	arch=('any')
}

package_freefall-vanilla() {
	provides=('freefall-bin')
	conflicts=('freefall-cromnix')
	depends=('freefall-init' 'glibc')
	cd "$srcdir/linux-$pkgver/tools/laptop/freefall"
	make DESTDIR="$pkgdir" SBINDIR=bin install
}

package_freefall-cromnix() {
	provides=('freefall-bin')
	conflicts=('freefall-vanilla')
	depends=('openrc' 'glibc')
	install=freefall-service.install
	cd "$srcdir/linux-$pkgver/tools/laptop/freefall"
	make DESTDIR="$pkgdir" SBINDIR=bin install

	install -v -m755 -d ${pkgdir}/etc/openrc/init.d
	install -v -m755 -d ${pkgdir}/etc/openrc/conf.d
	install -v -m755 ${srcdir}/freefall.initd ${pkgdir}/etc/openrc/init.d/freefall
	install -v -m644 ${srcdir}/freefall.confd ${pkgdir}/etc/openrc/conf.d/freefall

	install -Dm644 ${srcdir}/freefall-service.hook ${pkgdir}/usr/share/libalpm/hooks/freefall-service.hook
	install -Dm755 ${srcdir}/freefall-service.script ${pkgdir}/usr/share/libalpm/scripts/freefall-service
}

package_freefall-openrc() {
	pkgdesc="OpenRC init scripts for gpm"
	arch=('any')
	depends=('openrc')
	provides=('freefall-init')
	conflicts=('freefall-cromnix')
	install -v -m755 -d ${pkgdir}/etc/openrc/init.d
	install -v -m755 -d ${pkgdir}/etc/openrc/conf.d
	install -v -m755 ${srcdir}/freefall.initd ${pkgdir}/etc/openrc/init.d/freefall
	install -v -m644 ${srcdir}/freefall.confd ${pkgdir}/etc/openrc/conf.d/freefall
}
