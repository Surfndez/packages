# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=git
pkgver=2.14.0
pkgrel=2
pkgdesc="Git is a free and open source, distributed version control system designed to handle everything from small to very large projects with speed and efficiency."
arch=('x86_64')
url="https://git-scm.com/"
license=('GPL')
depends=('libcurl' 'expat' 'perl' 'openssl' 'pcre')
makedepends=('python2' 'xmlto' 'asciidoc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/git/git/archive/v$pkgver.tar.gz")
md5sums=('325b2f6ff163a6f3e4caa9c7747ce5a7')

# todo: rebuild with pcre2 and 'perl-error'

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make configure
    ./configure --prefix=/usr \
        --with-gitconfig=/etc/gitconfig \
        --enable-jit \
        --libexecdir=/usr/lib/git-core
    make
    make html
    make man
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make test
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    make DESTDIR="$pkgdir" install
    make DESTDIR="$pkgdir" install-man
    make DESTDIR="$pkgdir" htmldir=/usr/share/doc/${pkgname}-${pkgver} install-html

	find ${pkgdir}/usr/share/locale -type d -exec chmod 755 {} +
}
