# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=git
pkgver=2.14.2
pkgrel=1
pkgdesc="Git is a free and open source, distributed version control system designed to handle everything from small to very large projects with speed and efficiency."
arch=('x86_64')
url="https://git-scm.com/"
license=('GPL')
depends=('libcurl' 'expat' 'perl' 'openssl' 'pcre2')
makedepends=('python2' 'xmlto' 'asciidoc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/git/git/archive/v$pkgver.tar.gz")
md5sums=('c91dec69b114c7803378475277565673')

# todo: rebuild with 'perl-error'

build() {
    cd "$srcdir/$pkgname-$pkgver"
    export PYTHON_PATH='/usr/bin/python2'
    make configure
    ./configure --prefix=/usr \
        --with-gitconfig=/etc/gitconfig \
        --with-libpcre2 \
        --libexecdir=/usr/lib/git-core
    make
    make html
    make man
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make test
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    make DESTDIR="$pkgdir" \
		CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
		USE_LIBPCRE2=1 \
		NO_CROSS_DIRECTORY_HARDLINKS=1 \
		MAN_BOLD_LITERAL=1 \
		INSTALLDIRS=vendor \
		install
    make DESTDIR="$pkgdir" install-man
    make DESTDIR="$pkgdir" install-doc
    make DESTDIR="$pkgdir" htmldir=/usr/share/doc/${pkgname}-${pkgver} install-html

	find ${pkgdir}/usr/share/locale -type d -exec chmod 755 {} +
}
