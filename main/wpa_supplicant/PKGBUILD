# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=wpa_supplicant
pkgname=('wpa_supplicant' 'wpa_supplicant-openrc')
pkgver=2.6
pkgrel=6
pkgdesc="WPA Supplicant is a Wi-Fi Protected Access (WPA) client and IEEE 802.1X supplicant."
arch=('x86_64')
url="https://w1.fi/wpa_supplicant/"
license=('GPL')
makedepends=('openssl' 'libnl' 'dbus' 'openrc-services' 'libxml2')
source=("https://w1.fi/releases/$pkgbase-$pkgver.tar.gz"
		"https://gitweb.gentoo.org/repo/gentoo.git/plain/net-wireless/wpa_supplicant/files/wpa_supplicant-init.d"
		"https://gitweb.gentoo.org/repo/gentoo.git/plain/net-wireless/wpa_supplicant/files/wpa_supplicant-conf.d"
		'wpa_cli.sh'
		'wpa_supplicant.conf'
		'config')
md5sums=('091569eb4440b7d7f2b4276dbfc03c3c'
         '81e607b82ff5965f9ea6c004ea7287c2'
         'bc117427f2c538439f3f1481a028ee06'
         '3ee21a12dcca2736f23d6dbd5a89da35'
         '6198b16d60af3a0ff96f88688eea3144'
         '3124d9a6958b0c83c83bd03a72aee89b')

prepare() {
	cp -v ${srcdir}/config "$srcdir/$pkgbase-$pkgver/wpa_supplicant/.config"
}

build() {
	cd "$srcdir/$pkgbase-$pkgver/wpa_supplicant"
	export CFLAGS="${CPPFLAGS} ${CFLAGS}"
	make BINDIR=/usr/bin LIBDIR=/usr/lib
	make BINDIR=/usr/bin LIBDIR=/usr/lib eapol_test
}

package_wpa_supplicant() {
	depends=('wpa_supplicant-init' 'openssl' 'libnl')
	backup=('etc/wpa_supplicant/wpa_supplicant.conf')
	cd "$srcdir/$pkgbase-$pkgver/wpa_supplicant"

	make BINDIR=/usr/bin LIBDIR=/usr/lib DESTDIR="${pkgdir}" install

	install -v -m755 -d ${pkgdir}/usr/bin
	install -v -m755 -d ${pkgdir}/usr/share/man/man5
	install -v -m755 -d ${pkgdir}/usr/share/man/man8
	install -v -m755 wpa_{cli,passphrase,supplicant} ${pkgdir}/usr/bin/
	install -v -m755 eapol_test ${pkgdir}/usr/bin/
	install -v -m644 doc/docbook/wpa_supplicant.conf.5 ${pkgdir}/usr/share/man/man5/
	install -v -m644 doc/docbook/wpa_{cli,passphrase,supplicant}.8 ${pkgdir}/usr/share/man/man8/

	install -v -m755 -d ${pkgdir}/etc/wpa_supplicant
	install -v -m644 ${srcdir}/wpa_supplicant.conf ${pkgdir}/etc/wpa_supplicant/wpa_supplicant.conf
	install -v -m644 wpa_supplicant.conf ${pkgdir}/etc/wpa_supplicant/wpa_supplicant.conf.example

	install -v -m755 -d ${pkgdir}/usr/share/dbus-1/system-services
	install -v -m644 dbus/fi.{epitest.hostap.WPASupplicant,w1.wpa_supplicant1}.service ${pkgdir}/usr/share/dbus-1/system-services
	install -v -d -m755 ${pkgdir}/etc/dbus-1/system.d
	install -v -d -m644 dbus/dus-wpa_supplicant.conf ${pkgdir}/etc/dbus-1/system.d/wpa_supplicant.conf

	install -v -m755 ${srcdir}/wpa_cli.sh ${pkgdir}/etc/wpa_supplicant/wpa_cli.sh
}

package_wpa_supplicant-openrc() {
	pkgdesc="OpenRC init scripts for wpa_supplicant"
	arch=('any')
	depends=('openrc' 'openrc-services')
	provides=('wpa_supplicant-init')
	install -v -m755 -d ${pkgdir}/etc/openrc/{init.d,conf.d}
	install -v -m755 ${srcdir}/wpa_supplicant-init.d ${pkgdir}/etc/openrc/init.d/wpa_supplicant
	install -v -m644 ${srcdir}/wpa_supplicant-conf.d ${pkgdir}/etc/openrc/conf.d/wpa_supplicant
	
	install -Dm644 /usr/share/openrc-services/hooks/svc-openrc-install.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-install.hook
	install -Dm644 /usr/share/openrc-services/hooks/svc-openrc-upgrade.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-upgrade.hook
	install -Dm644 /usr/share/openrc-services/hooks/svc-openrc-remove.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-remove.hook
	install -Dm755 /usr/share/openrc-services/scripts/svc-openrc.script "${pkgdir}"/usr/share/libalpm/scripts/${pkgbase}-openrc
	
	sed -e "s/@svc@/${pkgbase}/" -i "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-{install,upgrade,remove}.hook
	sed -e "s/@svc_enable@/'${pkgbase}'/" \
		-e "s/@svc_run@/'${pkgbase}'/" \
		-e "s/@svc_remove@/'${pkgbase}'/" \
		-e "s/@svlevel@/default/" \
		-i "${pkgdir}"/usr/share/libalpm/scripts/${pkgbase}-openrc
}
