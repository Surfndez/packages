# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=pamac-classic
pkgver=5.2.0
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm"
arch=('x86_64')
url="https://github.com/cromnix/pamac-classic"
license=('GPL3')
depends=('glib' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3' 'gtk3'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'gnutls')
makedepends=('gettext' 'itstool' 'vala>=0.36' 'atk-dev')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
			'mate-polkit: needed for authentification in Mate'
			'lxsession: needed for authentification in Xfce, LXDE etc.'
			'pamac-tray-appindicator: tray icon for KDE')
conflicts=('pamac')
backup=('etc/pamac.conf')
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
md5sums=('2d898d5b7e82d93c9239669fa5a4830e')

prepare() {
	cd "$srcdir/${pkgname}-${pkgver}"
	# patches here
	#patch -Np1 -i "${srcdir}"/disable-aur.patch
	#patch -Np1 -i "${srcdir}"/category-fix.patch
}

build() {
	cd "$srcdir/${pkgname}-${pkgver}"
	make all
}

package() {
	cd "$srcdir/${pkgname}-${pkgver}"
	make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install
	rm -rf "${pkgdir}"/etc/systemd
	rm -rf "${pkgdir}"/usr/lib/systemd
}
