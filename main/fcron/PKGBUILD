# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=fcron
pkgname=('fcron' 'fcron-openrc')
pkgver=3.2.1
pkgrel=4
pkgdesc='The Fcron package contains a periodical command scheduler'
arch=('x86_64')
url='http://fcron.free.fr'
license=('GPL')
makedepends=('fcron' 'nano' 'openrc-services')
source=("http://fcron.free.fr/archives/${pkgname}-${pkgver}.src.tar.gz"
		"https://gitweb.gentoo.org/repo/gentoo.git/plain/sys-process/fcron/files/fcron.init.4"
		"https://gitweb.gentoo.org/repo/gentoo.git/plain/sys-process/cronbase/files/run-crons-0.3.7"
		'fcrontab'
		'crontab')
md5sums=('bd4996e941a40327d11efc5e3fd1f839'
         '6f2a4b161349abd2cfee8baf306443c6'
         'c5caba6f41a37c3c5dff46ca5193e323'
         '731c1325a859235347e1956fd72aa36a'
         'a0e7c32bef48f80023b708d8cc766728')

# todo: rebuild with vi as requirement

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i -e 's:/etc/fcrontab:/etc/fcron/fcrontab:' script/check_system_crontabs.sh
	sed -i -e 's:/usr/libexec/fcron:/usr/bin/fcron:' "${srcdir}"/fcron.init.4
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--sysconfdir=/etc/fcron \
		--sbindir=/usr/bin \
		--localstatedir=/var \
		--without-sendmail \
		--with-boot-install=no \
		--with-editor=/usr/bin/nano \
		--with-systemdsystemunitdir=no 
	make
}

package_fcron() {
	depends=('fcron-init')
	install=fcron.install
	cd "$srcdir/$pkgbase-$pkgver"
	make DESTDIR="$pkgdir" install
	
	install -v -m755 "${srcdir}"/run-crons-0.3.7 ${pkgdir}/usr/bin/run-crons
	install -v -m750 -d "${pkgdir}"/var/spool/cron/lastrun
	
	install -v -m640 "${srcdir}"/fcrontab ${pkgdir}/etc/fcron/fcrontab
	chown root:fcron ${pkgdir}/etc/fcron/fcrontab
	
	install -v -m644 "${srcdir}"/crontab ${pkgdir}/etc/crontab
	install -v -m755 -d "${pkgdir}"/etc/cron.d
	
	install -v -m755 -d "${pkgdir}"/usr/lib/fcron/
	install -v -m755 script/check_system_crontabs.sh ${pkgdir}/usr/lib/fcron/check_system_crontabs
	
	install -v -m750 -d "${pkgdir}"/etc/cron.{hourly,daily,weekly,monthly}
	
	rm -rf ${pkgdir}/var/run
}

package_fcron-openrc() {
	pkgdesc="OpenRC init scripts for fcron"
	arch=('any')
	depends=('openrc' 'openrc-services')
	optdepends=('vi: to edit fcrontab')
	provides=('fcron-init')
	install -v -m755 -d ${pkgdir}/etc/openrc/init.d
	install -v -m755 ${srcdir}/fcron.init.4 ${pkgdir}/etc/openrc/init.d/fcron

	install -Dm644 /usr/share/openrc-services/hooks/svc-openrc-install.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-install.hook
	install -Dm644 /usr/share/openrc-services/hooks/svc-openrc-upgrade.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-upgrade.hook
	install -Dm644 /usr/share/openrc-services/hooks/svc-openrc-remove.hook "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-remove.hook
	install -Dm755 /usr/share/openrc-services/scripts/svc-openrc.script "${pkgdir}"/usr/share/libalpm/scripts/${pkgbase}-openrc
	
	sed -e "s/@svc@/${pkgbase}/" -i "${pkgdir}"/usr/share/libalpm/hooks/${pkgbase}-openrc-{install,upgrade,remove}.hook
	sed -e "s/@svc_enable@/'${pkgbase}'/" \
		-e "s/@svc_run@/'${pkgbase}'/" \
		-e "s/@svc_remove@/'${pkgbase}'/" \
		-e "s/@svlevel@/default/" \
		-i "${pkgdir}"/usr/share/libalpm/scripts/${pkgbase}-openrc
}
