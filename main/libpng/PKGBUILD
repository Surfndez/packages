# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libpng
pkgver=1.6.32
pkgrel=1
pkgdesc='The libpng package contains libraries used by other programs for reading and writing PNG files'
arch=('x86_64')
url='http://www.libpng.org/pub/png/libpng.html'
license=('custom')
depends=('zlib' 'sh')
source=("http://downloads.sourceforge.net/libpng/${pkgname}-${pkgver}.tar.xz"
		"http://downloads.sourceforge.net/project/apng/libpng/libpng16/${pkgname}-${pkgver}-apng.patch.gz")
md5sums=('e01be057a9369183c959b793a685ad15'
         '86b44700cfcf5000b99bfff7d28f4345')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -Np0 -i ${srcdir}/${pkgname}-${pkgver}-apng.patch
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr \
		--disable-static
	make
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make check
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	mkdir -pv ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
	cp -v README libpng-manual.txt ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
}
