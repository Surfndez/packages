# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=sqlite
pkgver=3.20.0
_srcver=3200000
pkgrel=1
pkgdesc='The SQLite package is a software library that implements a self-contained, serverless, zero-configuration, transactional SQL database engine'
arch=('x86_64')
url='http://www.sqlite.org/'
license=('custom:Public Domain')
depends=('tcl' 'readline')
makedepends=('tcl' 'readline')
options=('!emptydirs' '!makeflags')
source=("http://sqlite.org/2017/${pkgname}-autoconf-${_srcver}.tar.gz"
		"http://sqlite.org/2017/${pkgname}-doc-${_srcver}.zip")
md5sums=('e262a28b73cc330e7e83520c8ce14e4d'
         'ed8f9d7dfb410853619e195af2e73250')

# todo: rebuild with libedit and unzip for docs

build() {
	cd "${srcdir}/${pkgname}-autoconf-${_srcver}"

	export CPPFLAGS="$CPPFLAGS -DSQLITE_ENABLE_FTS3=1 \
		-DSQLITE_ENABLE_COLUMN_METADATA=1 \
		-DSQLITE_ENABLE_UNLOCK_NOTIFY=1 \
		-DSQLITE_SECURE_DELETE=1 \
		-DSQLITE_ENABLE_DBSTAT_VTAB=1"

	./configure --prefix=/usr \
		--disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-autoconf-${_srcver}"
	make DESTDIR="${pkgdir}" install
	install -v -m755 -d "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
	cp -v -R "${srcdir}"/${pkgname}-doc-${_srcver}/* "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
}
