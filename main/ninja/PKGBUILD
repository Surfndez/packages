# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=ninja
pkgver=1.7.2
pkgrel=1
pkgdesc='Ninja is a small build system with a focus on speed'
arch=('x86_64')
url='https://ninja-build.org/'
license=('Apache')
depends=('gcc-libs')
makedepends=('python2' 'asciidoc')
source=("${pkgname}-${pkgver}::https://github.com/ninja-build/ninja/archive/v${pkgver}.tar.gz")
md5sums=('7b482218757acbaeac4d4d54a3cd94e1')

# todo: add emacs and config later
# doxgen docs

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python2 ./configure.py --bootstrap
	./ninja manual
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python2 ./configure.py
	./ninja ninja_test
	./ninja_test --gtest_filter=-SubprocessTest.SetWithLots
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -vdm755 "${pkgdir}/usr/bin"
	install -vDm755 ninja "${pkgdir}/usr/bin/ninja"
	install -vDm644 misc/ninja.vim "${pkgdir}/usr/share/vim/vim74/syntax/ninja.vim"
	install -vDm644 misc/bash-completion "${pkgdir}/usr/share/bash-completion/completions/ninja"
	install -vDm644 misc/zsh-completion "${pkgdir}/usr/share/zsh/site-functions/_ninja"
	install -vDm644 doc/manual.html "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/manual.html"
}
