# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libxml2
pkgver=2.9.4
pkgrel=1
pkgdesc="The libxml2 package contains libraries and utilities used for parsing XML files."
arch=('x86_64')
url="http://www.xmlsoft.org/"
license=('MIT')
depends=('zlib' 'readline' 'ncurses' 'xz')
makedepends=('python')
options=(!makeflags)
source=("http://xmlsoft.org/sources/$pkgname-$pkgver.tar.gz"
        "http://www.w3.org/XML/Test/xmlts20130923.tar.gz")
md5sums=('ae249165c173b1ff386ee8ad676815f5'
         'ef8fbe0c92efd9a60d7f320e211d10b2')

prepare() {
    cd ${srcdir}
    cp -Rv xmlconf "$srcdir/$pkgname-$pkgver"
    cd "$srcdir/$pkgname-$pkgver"
    sed -i "/seems to be moved/s/^/#/" ltmain.sh
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr \
        --disable-static \
        --with-history
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check > check.log
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
