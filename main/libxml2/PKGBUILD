# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=libxml2
pkgver=2.9.5
pkgrel=1
pkgdesc="The libxml2 package contains libraries and utilities used for parsing XML files."
arch=('x86_64')
url="http://www.xmlsoft.org/"
license=('MIT')
depends=('zlib' 'readline' 'ncurses' 'xz' 'icu')
makedepends=('python' 'python2')
options=(!makeflags)
source=("http://xmlsoft.org/sources/${pkgname}-${pkgver}.tar.gz"
        "http://www.w3.org/XML/Test/xmlts20130923.tar.gz")
md5sums=('5ce0da9bdaa267b40c4ca36d35363b8b'
         'ef8fbe0c92efd9a60d7f320e211d10b2')

prepare() {
    cd "${srcdir}"
	mkdir build-py3
	mkdir build-py2
	mv xmlconf build-py2/
}

build() {
	cd "${srcdir}"/build-py2
	../${pkgname}-${pkgver}/configure --prefix=/usr \
		--with-threads \
		--with-history \
		--with-python=/usr/bin/python2 \
		--with-icu
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
	make

	cd "${srcdir}"/build-py3
	../${pkgname}-${pkgver}/configure --prefix=/usr \
		--with-threads \
		--with-history \
		--with-python=/usr/bin/python \
		--with-icu
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
	PYTHONHASHSEED=0 make
    make
}

check() {
	cd "${srcdir}"/build-py2
    make check > check.log
}

package() {
	cd "${srcdir}"/build-py2
	find doc -type f -exec chmod 0644 {} \;
    make DESTDIR="${pkgdir}" install
    
	cd "${srcdir}"/build-py3/python
	PYTHONHASHSEED=0 make DESTDIR="${pkgdir}" install
}
