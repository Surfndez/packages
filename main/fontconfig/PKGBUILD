# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=fontconfig
pkgver=2.12.4
pkgrel=2
pkgdesc='Contains a library and support programs used for configuring and customizing font access'
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/fontconfig/'
license=('custom')
depends=('expat' 'freetype')
makedepends=('python2' 'gperf')
source=("http://www.freedesktop.org/software/fontconfig/release/${pkgname}-${pkgver}.tar.bz2")
md5sums=('29105662c7d319720e0088a0ac53f494')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	autoreconf -fi
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-docs \
		--docdir=/usr/share/doc/${pkgname}-${pkgver}
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -v -dm755 ${pkgdir}/usr/share/{man/man{1,3,5},doc/${pkgname}-${pkgver}/fontconfig-devel}
	install -v -m644 fc-*/*.1 ${pkgdir}/usr/share/man/man1
	install -v -m644 doc/*.3 ${pkgdir}/usr/share/man/man3
	install -v -m644 doc/fonts-conf.5 ${pkgdir}/usr/share/man/man5
	install -v -m644 doc/fontconfig-devel/* ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/fontconfig-devel
	install -v -m644 doc/*.{pdf,sgml,txt,html} ${pkgdir}/usr/share/doc/${pkgname}-${pkgver}
}
