# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=boost
pkgver=1.65.0
_pkgver=${pkgver//./_}
pkgrel=1
pkgdesc='Boost provides a set of free peer-reviewed portable C++ source libraries'
arch=('x86_64')
url='http://www.boost.org/'
license=('custom')
depends=('bzip2' 'zlib' 'icu')
makedepends=('icu' 'python' 'python2' 'bzip2' 'zlib' 'which')
checkdepends=('python2')
optepends=('python: for python bindings'
			'python2: for python2 bindings')
options=('staticlibs')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}_${_pkgver}.tar.bz2")
md5sums=('5512d3809801b0a1b9dd58447b70915d')

# todo: split and depends, if built with openmpi I beleive there will be a binary included as well
# rebuild using a staging folder

prepare() {
	cd "${srcdir}/${pkgname}_${_pkgver}"
	if [ -d "${srcdir}/install" ]; then
		rm -rf "${srcdir}/install"
	fi
	mkdir -p "${srcdir}/install"
}

build() {
	cd "${srcdir}/${pkgname}_${_pkgver}"
	local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"
	
	install -dm755 "${srcdir}"/install/share/boostbook
	cp -a tools/boostbook/{xsl,dtd} "${srcdir}"/install/share/boostbook/

	./bootstrap.sh --with-toolset=gcc \
		--with-icu \
		--with-python=/usr/bin/python2
	
	# copy the binary to the install folder
	install -Dm755 tools/build/src/engine/bin.linuxx86_64/b2 "${srcdir}"/install/bin/b2
	ln -s b2 "${srcdir}"/install/bin/bjam
		
	./b2 variant=release \
		debug-symbols=off \
		threading=multi \
		runtime-link=shared \
		link=shared,static \
		toolset=gcc \
		python=2.7 \
		cflags="${CPPFLAGS} ${CFLAGS} -fPIC -O3" \
		cxxflags="${CPPFLAGS} ${CXXFLAGS} -std=c++14 -fPIC -O3" \
		linkflags="${LDFLAGS}" \
		--layout=system \
		--prefix="${srcdir}/install" \
		install \
		${JOBS}
	
	sed -e '/using python/ s@;@: /usr/include/python${PYTHON_VERSION/3*/${PYTHON_VERSION}m} ;@' -i bootstrap.sh
	./bootstrap.sh --with-toolset=gcc \
		--with-icu \
		--with-python=/usr/bin/python3 \
		--with-libraries=python
	./b2 clean
	./b2 variant=release \
		debug-symbols=off \
		threading=multi \
		runtime-link=shared \
		link=shared,static \
		toolset=gcc \
		python=3.6 \
		cflags="${CPPFLAGS} ${CFLAGS} -fPIC -O3" \
		cxxflags="${CPPFLAGS} ${CXXFLAGS} -std=c++14 -fPIC -O3" \
		linkflags="${LDFLAGS}" \
		--layout=system \
		--prefix="${srcdir}/install/python3" \
		--with-python \
		install \
		${JOBS}
		
		cp -a "${srcdir}"/install/python3/lib/libboost_* "${srcdir}"/install/lib
		rm -rf "${srcdir}"/install/python3
}

check() {
	cd "${srcdir}/${pkgname}_${_pkgver}/tools/build/test"
	python test_all.py
}

package() {
	cd "${srcdir}/${pkgname}_${_pkgver}"
	
	install -dm755 "${pkgdir}"/usr
	cp -a "${srcdir}"/install/{bin,include,lib,share} "${pkgdir}"/usr
	
	install -Dm644 LICENSE_1_0.txt "${pkgdir}"/usr/share/licenses/boost/LICENSE
}
