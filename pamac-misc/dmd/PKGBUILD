# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=dmd
pkgname=('dmd' 'dmd-docs' 'libphobos')
pkgdesc='D programming language compiler and standard library'
groups=('dlang' 'dlang-dmd')
pkgver=2.075.0
pkgrel=1
arch=('x86_64')
url='http://www.dlang.org'
makedepends=('dmd')
source=("dmd-${pkgver}.tar.gz::https://github.com/dlang/dmd/archive/v${pkgver}.tar.gz"
		"druntime-${pkgver}.tar.gz::https://github.com/dlang/druntime/archive/v${pkgver}.tar.gz"
		"phobos-${pkgver}.tar.gz::https://github.com/dlang/phobos/archive/v${pkgver}.tar.gz"
		"http://downloads.dlang.org/releases/2.x/${pkgver}/dmd.${pkgver}.linux.tar.xz"
		'dmd.conf'
		'dmd-doc.desktop')
sha256sums=('9c18019485eb65a11cb60645701fa0a35aa7f910f5a399a897fa8297c14c3df6'
            'a64e5fa94d7b367b3e9d08b03c460c10de5dcced3418bbadeb53707a0a8d2bfd'
            '0d93d3d47be24995a1b1e97f0e04e7625d9e8b567e8c4a70d675af077f368f48'
            '3f9cc1e3a467b4917c045f179f5eb9eaf7d5f350e8cfe84ea3496fe29f18fb5e'
            '3d639e89528fed1da90006f4dfb2b0fdc41308da5a96d953381ff4ccf257c035'
            '19c9cd3670bdef7fbde88de36c459d940e6b7bacc2a5e48aefbfd1b2c1895e07')
noextract=("dmd.${pkgver}.linux.tar.xz")

prepare() {
	tar xfJ "dmd.${pkgver}.linux.tar.xz" dmd2/html
	ln -sv "${srcdir}"/dmd-${pkgver} "${srcdir}"/dmd
	ln -sv "${srcdir}"/druntime-${pkgver} "${srcdir}"/druntime
	ln -sv "${srcdir}"/phobos-${pkgver} "${srcdir}"/phobos
}

build() {
	cd "${srcdir}"/dmd-${pkgver}
	make -f posix.mak RELEASE=1 PIC=1

	cd "${srcdir}"/druntime-${pkgver}
	make -f posix.mak DMD="${srcdir}"/dmd-${pkgver}/src/dmd RELEASE=1 PIC=1

	cd "${srcdir}"/phobos-${pkgver}
	make -f posix.mak DMD="${srcdir}"/dmd-${pkgver}/src/dmd RELEASE=1 PIC=1
}

package_dmd() {
	pkgdesc='The D programming language reference compiler'
	backup=('etc/dmd.conf')
	depends=('gcc' 'libphobos')
	optdepends=('dtools: collection of useful utilities for development in D'
		'gcc-multilib: to cross-compile 32-bit applications'
		'dmd-docs: documentation and sample code for D')
	license=('custom')

	install -Dm755 dmd-${pkgver}/src/dmd "${pkgdir}"/usr/bin/dmd

	mkdir -p "${pkgdir}"/etc
	install -Dm644 dmd.conf "${pkgdir}"/etc/dmd.conf

	mkdir -p "${pkgdir}"/usr/share/man/man1
	mkdir -p "${pkgdir}"/usr/share/man/man5
	cp -r dmd-${pkgver}/docs/man/man1/* "${pkgdir}"/usr/share/man/man1/
	cp -r dmd-${pkgver}/docs/man/man5/* "${pkgdir}"/usr/share/man/man5/

	install -Dm644 dmd-${pkgver}/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

	find "${pkgdir}"/usr -type f | xargs chmod 0644
	chmod 755 "${pkgdir}"/usr/bin/*
}

package_dmd-docs() {
	pkgdesc="Documentation and sample code for D programming language"
	depends=('dmd')
	license=('custom')

	mkdir -p "${pkgdir}"/usr/share/applications
	install -Dm644 dmd-doc.desktop "${pkgdir}"/usr/share/applications/dmd-doc.desktop

	mkdir -p "${pkgdir}"/usr/share/d/samples/
	cp -r dmd-${pkgver}/samples/* "${pkgdir}"/usr/share/d/samples/

	mkdir -p "${pkgdir}"/usr/share/d/html
	cp -r dmd2/html/* "${pkgdir}"/usr/share/d/html/

	install -Dm644 dmd-${pkgver}/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_libphobos() {
	pkgdesc='The Phobos standard library for D programming language'
	options=('staticlibs')
	depends=('gcc-libs')
	license=('custom')

	mkdir -p "${pkgdir}"/usr/lib
	install -Dm644 phobos-${pkgver}/generated/linux/release/*/libphobos2.{a,so} "${pkgdir}"/usr/lib/
	install -Dm644 phobos-${pkgver}/LICENSE_1_0.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

	mkdir -p "${pkgdir}"/usr/include/dlang/dmd
	cp -r phobos-${pkgver}/{*.d,etc,std} "${pkgdir}"/usr/include/dlang/dmd
	cp -r druntime-${pkgver}/import/* "${pkgdir}"/usr/include/dlang/dmd/

	find "${pkgdir}"/usr -type f | xargs chmod 0644

	install -Dm644 druntime-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE-druntime
}
