# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=appstream-glib
pkgver=0.6.8
pkgrel=1
pkgdesc='The Appstream-GLib package contains a library that provides GObjects and helper methods to make it easy to read and write AppStream metadata'
arch=('x86_64')
url='https://people.freedesktop.org/~hughsient/appstream-glib/'
license=('LGPL')
depends=('gtk3' 'libyaml' 'pacman' 'gcab' 'libsoup' 'libstemmer')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc' 'gperf')
source=("http://people.freedesktop.org/~hughsient/${pkgname}/releases/${pkgname}-${pkgver}.tar.xz")
md5sums=('5231f3d8c1e1cde1d8afc1c54d0a7284')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--disable-static \
		--enable-gtk-doc \
		--enable-alpm \
		--enable-stemmer
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
