# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=libxt
_pkgname=libXt
pkgver=1.1.5
pkgrel=1
pkgdesc='X11 toolkit intrinsics library'
arch=('x86_64')
url='https://www.x.org/wiki/'
license=('custom')
depends=('libsm' 'libx11')
makedepends=('util-macros' 'xmlto' 'docbook-xml' 'asciidoc')
source=("http://ftp.x.org/pub/individual/lib/${_pkgname}-${pkgver}.tar.bz2")
md5sums=('8f5b5576fbabba29a05f3ca2226f74d3')

build() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static
	make
}

check() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make check
}

package() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}
