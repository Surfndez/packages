# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=xcb-proto
pkgver=1.12
pkgrel=1
pkgdesc='XML-XCB protocol descriptions'
arch=('any')
url='https://www.x.org/wiki/'
license=('custom')
makedepends=('python' 'libxml2')
source=("http://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.bz2"
		"http://www.linuxfromscratch.org/patches/blfs/svn/${pkgname}-${pkgver}-python3-1.patch"
		"http://www.linuxfromscratch.org/patches/blfs/svn/${pkgname}-${pkgver}-schema-1.patch")
md5sums=('14e60919f859560f28426a685a555962'
         '5061c890c10a2dd0b601b65fb31b0a38'
         '6ba5f4180cfbd94d2a5577fc3204e275')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-python3-1.patch
	patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-schema-1.patch
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var
	make
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make check
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}
