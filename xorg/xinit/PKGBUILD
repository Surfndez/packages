#Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=xinit
pkgver=1.3.4
pkgrel=1
pkgdesc='X.Org initialisation program'
arch=('x86_64')
url='https://xorg.freedesktop.org/'
license=('custom')
depends=('libx11' 'xauth' 'xrdb' 'xmodmap' 'inetutils')
makedepends=('util-macros')
optdepends=('twm: window manager'
			'xterm: terminal emulator'
			'xclock: x clock program')
backup=('etc/X11/xinit/xserverrc'
		'etc/X11/xinit/xinitrc')
options=('!emptydirs')
source=("https://www.x.org/archive/individual/app/${pkgname}-${pkgver}.tar.bz2"
		'xserverrc')
md5sums=('4e928452dfaf73851413a2d8b8c76388'
         'b79a1bb65fec11b58fc4efa51be51480')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}
	sed -e '/$serverargs $vtarg/ s/serverargs/: #&/' -i startx.cpp
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-xinitdir=/etc/X11/xinit
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -m644 ${srcdir}/xserverrc ${pkgdir}/etc/X11/xinit/xserverrc
}
