#Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=luit
pkgver=1.1.1
pkgrel=1
pkgdesc='Filter that can be run between an arbitrary application and a UTF-8 terminal emulator'
arch=('x86_64')
url='https://xorg.freedesktop.org/'
license=('custom')
depends=('libfontenc')
makedepends=('util-macros')
groups=('xorg-apps' 'xorg')
source=("https://www.x.org/archive/individual/app/${pkgname}-${pkgver}.tar.bz2")
md5sums=('c4a3664e08e5a47c120ff9263ee2f20c')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}
	sed -i -e "s@#ifdef HAVE_CONFIG_H@#ifdef _XOPEN_SOURCE\n#  undef _XOPEN_SOURCE\n#  define _XOPEN_SOURCE 600\n#endif\n\n&@" sys.c
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}
