# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=xterm
pkgver=330
pkgrel=1
pkgdesc='X Terminal Emulator'
arch=('x86_64')
url='http://invisible-island.net/xterm/'
license=('custom')
depends=('libxft' 'libxaw' 'ncurses' 'luit' 'xbitmaps' 'libxkbfile')
source=("ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver}.tgz"
		'XTerm')
md5sums=('9a6db7974aa4ccbb50eabebf72618467'
         '1fd0d9760491c5102a15d8077360655f')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}
	sed -i '/v0/{n;s/new:/new:kb=^?:/}' termcap
	printf '\tkbs=\\177,\n' >> terminfo
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	#export TERMINFO=/usr/share/terminfo
	./configure --prefix=/usr \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-own-terminfo=/usr/share/terminfo \
		--with-app-defaults=/usr/share/X11/app-defaults/
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -m0644 ${srcdir}/XTerm ${pkgdir}/usr/share/X11/app-defaults/XTerm
	install -m755 -d ${pkgdir}/usr/share/applications
	install -m644 ${srcdir}/${pkgname}-${pkgver}/{xterm,uxterm}.desktop ${pkgdir}/usr/share/applications/
}
