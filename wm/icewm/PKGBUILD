#Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=icewm
pkgname=('icewm' 'icewm-session')
pkgver=1.3.12
pkgrel=1
pkgdesc='IceWM is a window manager with the goals of speed, simplicity, and not getting in the way'
arch=('x86_64')
url='http://www.icewm.org/'
license=('LGPL')
makedepends=('mkfontdir' 'libxrandr' 'libxft' 'libsm' 'libxinerama' 'gdk-pixbuf')
source=("https://github.com/bbidulock/${pkgbase}/releases/download/${pkgver}/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('3e3b8904d53998c0f46414722d86457c')

# rebuild with alsa

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--docdir=/usr/share/${pkgbase}-${pkgver}
	make
}

package_icewm-session() {
	arch=('any')
	optdepends=('icewm: to use icewm')
	cd "${srcdir}/${pkgbase}-${pkgver}/lib"
	make DESTDIR="${pkgdir}" install-dist_xsessionDATA
	rm "${pkgdir}"/usr/share/xsessions/icewm.desktop
}


package_icewm() {
	groups=('wm')
	depends=('libxrandr' 'libxft' 'libsm' 'libxinerama' 'gdk-pixbuf')
	cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rm "${pkgdir}"/usr/share/xsessions/icewm{,-session}.desktop
}
