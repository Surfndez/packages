# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgbase=python-packaging
pkgname=(python-packaging python2-packaging)
pkgver=16.8
pkgrel=2
pkgdesc="Core utilities for Python packages"
arch=('any')
url="https://github.com/pypa/packaging"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyparsing' 'python2-pyparsing')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pretend' 'python2-pretend' 'python-coverage' 'python2-coverage')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pypa/packaging/archive/${pkgver}.tar.gz")
md5sums=('02c9a248368c7b9d3236cac349a67825')

prepare() {
  cp -a packaging-${pkgver}{,-py2}
}

build() {
  cd "$srcdir"/packaging-${pkgver}
  python setup.py build

  cd "$srcdir"/packaging-${pkgver}-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/packaging-${pkgver}
  python setup.py ptr

  cd "$srcdir"/packaging-${pkgver}-py2
  python2 setup.py ptr
}

package_python-packaging() {
  depends=('python-pyparsing' 'python-six')

  cd "$srcdir"/packaging-${pkgver}
  python setup.py install --root "$pkgdir"
}

package_python2-packaging() {
  depends=('python2-pyparsing' 'python2-six')

  cd "$srcdir"/packaging-${pkgver}-py2
  python2 setup.py install --root "$pkgdir"
}
