# Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=nano
pkgver=2.8.6
_pkgver=${pkgver%.*}
pkgrel=1
pkgdesc="Pico editor clone with enhancements"
arch=('x86_64')
license=('GPL')
url="http://www.nano-editor.org"
depends=('ncurses' 'file' 'sh')
backup=('etc/nanorc')
source=(http://www.nano-editor.org/dist/v${_pkgver}/${pkgname}-${pkgver}.tar.gz)
md5sums=('e05d83c0f1687b733322f806c080d256')

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--enable-utf8 \
		--enable-color \
		--enable-nanorc \
		--enable-multibuffer \
		--disable-wrapping-as-root \
		--docdir=/usr/share/doc/nano-${pkgver}
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -v -m644 doc/nano.html ${pkgdir}/usr/share/doc/nano-${pkgver}
	install -DTm644 ${srcdir}/${pkgname}-${pkgver}/doc/sample.nanorc ${pkgdir}/etc/nanorc

	sed -e 's|# set constantshow|set constantshow|' \
		-e 's|# set tabsize 8|set tabsize 4|' \
		-e 's|# set multibuffer|set multibuffer|' \
		-e 's|# set nowrap|set nowrap|' \
		-e 's|# include "/usr/share/nano/\*.nanorc"|include "/usr/share/nano/*.nanorc"|' \
		-i ${pkgdir}/etc/nanorc
}
