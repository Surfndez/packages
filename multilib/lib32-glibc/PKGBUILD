#Maintainer: Chris Cromer <cromer@cromnix.org>

pkgname=lib32-glibc
_pkgname=glibc
pkgver=2.25
pkgrel=1
pkgdesc="The main C library."
arch=('x86_64')
url="https://www.gnu.org/software/libc/"
license=('GPL' 'LGPL')
depends=('linux-api-headers>=4.11' 'cromnix' 'tzdata')
makedepends=('gcc-multilib>=7.1.0')
options=('!strip' 'staticlibs')
source=("https://ftp.gnu.org/gnu/$_pkgname/$_pkgname-$pkgver.tar.xz"
        "http://www.linuxfromscratch.org/patches/lfs/8.0/$_pkgname-$pkgver-fhs-1.patch")
md5sums=('1496c3bf41adf9db0ebd0af01f202eed'
         '9a5997c3452909b1769918c759eff8a2')

prepare() {
    cd "$srcdir/$_pkgname-$pkgver"
    patch -Np1 -i "$srcdir/glibc-${pkgver}-fhs-1.patch"

    mkdir -pv "$srcdir/$_pkgname-$pkgver/build"
}

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"

    cd "$srcdir/$_pkgname-$pkgver/build"

    echo "slibdir=/usr/lib32" >> configparms
    echo "rtlddir=/usr/lib32" >> configparms

    export CFLAGS="${CFLAGS} -mno-tls-direct-seg-refs"
    CFLAGS=${CFLAGS/-fstack-protector-strong/}
    CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}

    ../configure --prefix=/usr \
        --libdir=/usr/lib32 \
        --libexecdir=/usr/lib32 \
        --disable-profile \
        --enable-kernel=2.6.32 \
        --enable-obsolete-rpc \
        --host=i686-pc-linux-gnu

    echo "build-programs=no" >> configparms
    make

    sed -i "/build-programs=/s#no#yes#" configparms
    echo "CC += -fstack-protector-strong" >> configparms
    echo "CXX += -fstack-protector-strong" >> configparms

    make
}

check() {
    cd "$srcdir/$_pkgname-$pkgver/build"
    make check || true
}

package() {
    cd "$srcdir/$_pkgname-$pkgver/build"

    make install_root=${pkgdir} install

    rm -rf ${pkgdir}/{etc,sbin,usr/{bin,sbin,share},var}
    find ${pkgdir}/usr/include -type f -not -name '*-32.h' -delete

    mkdir ${pkgdir}/usr/lib
    ln -s ../lib32/ld-linux.so.2 ${pkgdir}/usr/lib/

    ln -s ../lib/locale "$pkgdir/usr/lib32/locale"
}
