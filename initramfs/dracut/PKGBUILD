pkgname=dracut
pkgver=046
pkgrel=1
pkgdesc='Generic, modular, cross-distribution initramfs generation tool'
arch=('x86_64')
url='https://dracut.wiki.kernel.org/'
license=('GPL2')
depends=('cpio' 'kbd' 'kmod' 'util-linux' 'bash')
makedepends=('docbook-xsl' 'asciidoc')
optdepends=('cryptsetup: Part of the Crypto setup'
	'dmraid: Part of the Raid setup'
	'lvm2: Part of the LVM setup'
	'mdadm: Part of the MDadmin thing'
	'kexec-tools: For switching kernels without reboot support')
conflicts=('lfs-initramfs' 'mkinitcpio')
provides=('initramfs')
backup=('etc/dracut.conf')
install='dracut.install'
source=("http://www.kernel.org/pub/linux/utils/boot/$pkgname/${pkgname}-${pkgver}.tar.xz"
		'dracut.hook'
		'dracut.script')
md5sums=('224b67e9bc079e013541a74e85659188'
         'c62478578bba8e30665f53e19b32c1a6'
         '9591005a87475483f4d080ac7707a9a9')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's|$(arch)|$(uname -m)|g' "${srcdir}/${pkgname}-${pkgver}/modules.d/90crypt/module-setup.sh"
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib/dracut \
		--bashcompletiondir=/usr/share/bash-completion/completions \
		--sysconfdir=/etc \
		--loginstalldir=/var/log/dracut
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 "${srcdir}"/dracut.hook "${pkgdir}"/usr/share/libalpm/hooks/60-dracut.hook
	install -Dm755 "${srcdir}"/dracut.script "${pkgdir}"/usr/share/libalpm/scripts/dracut

	# remove systemd
	rm -rf "${pkgdir}"/usr/lib/kernel
	rm -rf "${pkgdir}"/usr/lib/dracut/modules.d/{00systemd,98dracut-systemd,00systemd-bootchart,01systemd-initrd,02systemd-networkd}
}
