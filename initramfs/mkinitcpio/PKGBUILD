pkgname=mkinitcpio
pkgver=23
pkgrel=1
pkgdesc='Modular initramfs image creation utility'
arch=('any')
url='https://projects.archlinux.org/mkinitcpio.git/'
license=('GPL')
depends=('awk' 'kmod' 'util-linux' 'libarchive' 'coreutils' 'bash' 'findutils' 'grep' 'cromnix' 'gzip' 'mkinitcpio-busybox')
makedepends=('asciidoc')
conflicts=('dracut' 'lfs-initramfs')
provides=('initramfs')
install=mkinitcpio.install
optdepends=('xz: Use lzma or xz compression for the initramfs image'
			'bzip2: Use bzip2 compression for the initramfs image'
			'lzop: Use lzo compression for the initramfs image'
			'lz4: Use lz4 compression for the initramfs image'
			'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz"
		'initcpio_hooks'
		'initcpio_install'
		'remove-systemd.patch'
		'mkinitcpio.hook'
		'mkinitcpio.script'
		'0001-make-ldd-parsing-compatible-with-upstream-glibc-chan.patch')
md5sums=('ce81c73dd622e359ee732fa6022b9891'
         '2b47f602e35f80cb6b4e9bf92d5e8eed'
         'b37b3de609406e7005ed3d4adf21e60d'
         '12e4d23228134f2a36ebcf2ad0673792'
         '89ad39920893fe908b7b00cc3eeef17b'
         '017b80c9f5d041b4746edc9454a12a6e'
         '6337748b0467b3ca67db72af9b61eaf4')

prepare() {
	cd "${pkgname}-${pkgver}"
	patch -Np1 -i "${srcdir}"/0001-make-ldd-parsing-compatible-with-upstream-glibc-chan.patch
	patch -Np1 -i "${srcdir}"/remove-systemd.patch
}

check() {
	make -C "${pkgname}-${pkgver}" check
}

package() {
	make -C "${pkgname}-${pkgver}" DESTDIR="${pkgdir}" install
	
	install -Dm644 "${srcdir}"/initcpio_hooks "${pkgdir}"/usr/lib/initcpio/hooks/udev
	install -Dm644 "${srcdir}"/initcpio_install "${pkgdir}"/usr/lib/initcpio/install/udev
	
	install -Dm644 "${srcdir}"/mkinitcpio.hook "${pkgdir}"/usr/share/libalpm/hooks/60-mkinitcpio.hook
	install -Dm755 "${srcdir}"/mkinitcpio.script "${pkgdir}"/usr/share/libalpm/scripts/mkinitcpio
}
